asm85 8085 Assembler by nib

                     1: ;*************************************************************
                     2: ; 
                     3: ;                 TINY BASIC FOR INTEL 8080
                     4: ;                       VERSION 2.0
                     5: ;                     BY LI-CHEN WANG
                     6: ;                  MODIFIED AND TRANSLATED
                     7: ;                    TO INTEL MNEMONICS
                     8: ;                     BY ROGER RAUSKOLB
                     9: ;                      10 OCTOBER,1976
                    10: ;                        @COPYLEFT
                    11: ;                   ALL WRONGS RESERVED
                    12: ; 
                    13: ;*************************************************************
                    14: ; 
                    15: ; *** ZERO PAGE SUBROUTINES ***
                    16: ; 
                    17: ; THE 8080 INSTRUCTION SET LETS YOU HAVE 8 ROUTINES IN LOW
                    18: ; MEMORY THAT MAY BE CALLED BY RST N, N BEING 0 THROUGH 7.
                    19: ; THIS IS A ONE BYTE INSTRUCTION AND HAS THE SAME POWER AS
                    20: ; THE THREE BYTE INSTRUCTION CALL LLHH.  TINY BASIC WILL
                    21: ; USE RST 0 AS START AND RST 1 THROUGH RST 7 FOR
                    22: ; THE SEVEN MOST FREQUENTLY USED SUBROUTINES.
                    23: ; TWO OTHER SUBROUTINES (CRLF AND TSTNUM) ARE ALSO IN THIS
                    24: ; SECTION.  THEY CAN BE REACHED ONLY BY 3-BYTE CALLS.
                    25: ; 
                    26: 
(0000)              27:         ORG  0H
0000 31 00 c0       28: START:  LXI  SP,STACK                   ;*** COLD START ***
0003 3e ff          29:         MVI  A,0FFH
0005 c3 3a 06       30:         JMP  INIT
                    31: ;
0008 e3             32:         XTHL                            ;*** TSTC OR RST 1 ***
0009 ef             33:         RST  5                          ;IGNORE BLANKS AND
000a be             34:         CMP  M                          ;TEST CHARACTER
000b c3 68 00       35:         JMP  TC1                        ;REST OF THIS IS AT TC1
                    36: ;
000e 3e 0d          37: CRLF:   MVI  A,CR                       ;*** CRLF ***
                    38: ;
0010 f5             39:         PUSH PSW                        ;*** OUTC OR RST 2 ***
0011 3a 00 20       40:         LDA  OCSW                       ;PRINT CHARACTER ONLY
0014 b7             41:         ORA  A                          ;IF OCSW SWITCH IS ON
0015 c3 5c 06       42:         JMP  OC2                        ;REST OF THIS IS AT OC2
                    43: ;
0018 cd 6b 03       44:         CALL EXPR2                      ;*** EXPR OR RST 3 ***
001b e5             45:         PUSH H                          ;EVALUATE AN EXPRESSION
001c c3 29 03       46:         JMP  EXPR1                      ;REST OF IT AT EXPR1
001f 57             47:         DB   'W'
                    48: ;
0020 7c             49:         MOV  A,H                        ;*** COMP OR RST 4 ***
0021 ba             50:         CMP  D                          ;COMPARE HL WITH DE
0022 c0             51:         RNZ                             ;RETURN CORRECT C AND
0023 7d             52:         MOV  A,L                        ;Z FLAGS
0024 bb             53:         CMP  E                          ;BUT OLD A IS LOST
0025 c9             54:         RET
0026 41 4e          55:         DB   'AN'
                    56: ;
0028 1a             57: SS1:    LDAX D                          ;*** IGNBLK/RST 5 ***
0029 fe 20          58:         CPI  20H                        ;IGNORE BLANKS
002b c0             59:         RNZ                             ;IN TEXT (WHERE DE->)
002c 13             60:         INX  D                          ;AND RETURN THE FIRST
002d c3 28 00       61:         JMP  SS1                        ;NON-BLANK CHAR. IN A
                    62: ;
0030 f1             63:         POP  PSW                        ;*** FINISH/RST 6 ***
0031 cd ab 04       64:         CALL FIN                        ;CHECK END OF COMMAND
0034 c3 be 04       65:         JMP  QWHAT                      ;PRINT "WHAT?" IF WRONG
0037 47             66:         DB   'G'
                    67: ;
0038 ef             68:         RST  5                          ;*** TSTV OR RST 7 ***
0039 d6 40          69:         SUI  40H                        ;TEST VARIABLES
003b d8             70:         RC                              ;C:NOT A VARIABLE
003c c2 58 00       71:         JNZ  TV1                        ;NOT "@" ARRAY
003f 13             72:         INX  D                          ;IT IS THE "@" ARRAY
0040 cd 12 04       73:         CALL PARN                       ;@ SHOULD BE FOLLOWED
0043 29             74:         DAD  H                          ;BY (EXPR) AS ITS INDEX
0044 da 9f 00       75:         JC   QHOW                       ;IS INDEX TOO BIG?
0047 d5             76:         PUSH D                          ;WILL IT OVERWRITE
0048 eb             77:         XCHG                            ;TEXT?
0049 cd 51 04       78:         CALL SIZE                       ;FIND SIZE OF FREE
004c e7             79:         RST  4                          ;AND CHECK THAT
004d da ec 04       80:         JC   ASORRY                     ;IF SO, SAY "SORRY"
0050 21 87 3f       81:         LXI  H,VARBGN                   ;IF NOT GET ADDRESS
0053 cd 74 04       82:         CALL SUBDE                      ;OF @(EXPR) AND PUT IT
0056 d1             83:         POP  D                          ;IN HL
0057 c9             84:         RET                             ;C FLAG IS CLEARED
0058 fe 1b          85: TV1:    CPI  1BH                        ;NOT @, IS IT A TO Z?
005a 3f             86:         CMC                             ;IF NOT RETURN C FLAG
005b d8             87:         RC
005c 13             88:         INX  D                          ;IF A THROUGH Z
005d 21 87 3f       89:         LXI  H,VARBGN                   ;COMPUTE ADDRESS OF
0060 07             90:         RLC                             ;THAT VARIABLE
0061 85             91:         ADD  L                          ;AND RETURN IT IN HL
0062 6f             92:         MOV  L,A                        ;WITH C FLAG CLEARED
0063 3e 00          93:         MVI  A,0
0065 8c             94:         ADC  H
0066 67             95:         MOV  H,A
0067 c9             96:         RET
                    97: ;
                    98: ;TSTC:  XTHL                            ;*** TSTC OR RST 1 ***
                    99: ;       RST  5                          ;THIS IS AT LOC. 8
                   100: ;       CMP  M                          ;AND THEN JUMP HERE
0068 23            101: TC1:    INX  H                          ;COMPARE THE BYTE THAT
0069 ca 73 00      102:         JZ   TC2                        ;FOLLOWS THE RST INST.
006c c5            103:         PUSH B                          ;WITH THE TEXT (DE->)
006d 4e            104:         MOV  C,M                        ;IF NOT =, ADD THE 2ND
006e 06 00         105:         MVI  B,0                        ;BYTE THAT FOLLOWS THE
0070 09            106:         DAD  B                          ;RST TO THE OLD PC
0071 c1            107:         POP  B                          ;I.E., DO A RELATIVE
0072 1b            108:         DCX  D                          ;JUMP IF NOT =
0073 13            109: TC2:    INX  D                          ;IF =, SKIP THOSE BYTES
0074 23            110:         INX  H                          ;AND CONTINUE
0075 e3            111:         XTHL
0076 c9            112:         RET
                   113: ;
0077 21 00 00      114: TSTNUM: LXI  H,0                        ;*** TSTNUM ***
007a 44            115:         MOV  B,H                        ;TEST IF THE TEXT IS
007b ef            116:         RST  5                          ;A NUMBER
007c fe 30         117: TN1:    CPI  30H                        ;IF NOT, RETURN 0 IN
007e d8            118:         RC                              ;B AND HL
007f fe 3a         119:         CPI  3AH                        ;IF NUMBERS, CONVERT
0081 d0            120:         RNC                             ;TO BINARY IN HL AND
0082 3e f0         121:         MVI  A,0F0H                     ;SET B TO # OF DIGITS
0084 a4            122:         ANA  H                          ;IF H>255, THERE IS NO
0085 c2 9f 00      123:         JNZ  QHOW                       ;ROOM FOR NEXT DIGIT
0088 04            124:         INR  B                          ;B COUNTS # OF DIGITS
0089 c5            125:         PUSH B
008a 44            126:         MOV  B,H                        ;HL=10*HL+(NEW DIGIT)
008b 4d            127:         MOV  C,L
008c 29            128:         DAD  H                          ;WHERE 10* IS DONE BY
008d 29            129:         DAD  H                          ;SHIFT AND ADD
008e 09            130:         DAD  B
008f 29            131:         DAD  H
0090 1a            132:         LDAX D                          ;AND (DIGIT) IS FROM
0091 13            133:         INX  D                          ;STRIPPING THE ASCII
0092 e6 0f         134:         ANI  0FH                        ;CODE
0094 85            135:         ADD  L
0095 6f            136:         MOV  L,A
0096 3e 00         137:         MVI  A,0
0098 8c            138:         ADC  H
0099 67            139:         MOV  H,A
009a c1            140:         POP  B
009b 1a            141:         LDAX D                          ;DO THIS DIGIT AFTER
009c f2 7c 00      142:         JP   TN1                        ;DIGIT. S SAYS OVERFLOW
009f d5            143: QHOW:   PUSH D                          ;*** ERROR "HOW?" ***
00a0 11 a6 00      144: AHOW:   LXI  D,HOW
00a3 c3 c2 04      145:         JMP  ERROR
00a6 48 4f 57 3f   146: HOW:    DB   'HOW?'
00aa 0d            147:         DB   CR
00ab 4f 4b         148: OK:     DB   'OK'
00ad 0d            149:         DB   CR
00ae 57 48 41 54   150: WHAT:   DB   'WHAT?'
     3f 
00b3 0d            151:         DB   CR
00b4 53 4f 52 52   152: SORRY:  DB   'SORRY'
     59 
00b9 0d            153:         DB   CR
                   154: ;
                   155: ;*************************************************************
                   156: ;
                   157: ; *** MAIN ***
                   158: ;
                   159: ; THIS IS THE MAIN LOOP THAT COLLECTS THE TINY BASIC PROGRAM
                   160: ; AND STORES IT IN THE MEMORY.
                   161: ;
                   162: ; AT START, IT PRINTS OUT "(CR)OK(CR)", AND INITIALIZES THE
                   163: ; STACK AND SOME OTHER INTERNAL VARIABLES.  THEN IT PROMPTS
                   164: ; ">" AND READS A LINE.  IF THE LINE STARTS WITH A NON-ZERO
                   165: ; NUMBER, THIS NUMBER IS THE LINE NUMBER.  THE LINE NUMBER
                   166: ; (IN 16 BIT BINARY) AND THE REST OF THE LINE (INCLUDING CR)
                   167: ; IS STORED IN THE MEMORY.  IF A LINE WITH THE SAME LINE
                   168: ; NUMBER IS ALREADY THERE, IT IS REPLACED BY THE NEW ONE.  IF
                   169: ; THE REST OF THE LINE CONSISTS OF A CR ONLY, IT IS NOT STORED
                   170: ; AND ANY EXISTING LINE WITH THE SAME LINE NUMBER IS DELETED.
                   171: ;
                   172: ; AFTER A LINE IS INSERTED, REPLACED, OR DELETED, THE PROGRAM
                   173: ; LOOPS BACK AND ASKS FOR ANOTHER LINE.  THIS LOOP WILL BE
                   174: ; TERMINATED WHEN IT READS A LINE WITH ZERO OR NO LINE
                   175: ; NUMBER; AND CONTROL IS TRANSFERED TO "DIRECT".
                   176: ;
                   177: ; TINY BASIC PROGRAM SAVE AREA STARTS AT THE MEMORY LOCATION
                   178: ; LABELED "TXTBGN" AND ENDS AT "TXTEND".  WE ALWAYS FILL THIS
                   179: ; AREA STARTING AT "TXTBGN", THE UNFILLED PORTION IS POINTED
                   180: ; BY THE CONTENT OF A MEMORY LOCATION LABELED "TXTUNF".
                   181: ;
                   182: ; THE MEMORY LOCATION "CURRNT" POINTS TO THE LINE NUMBER
                   183: ; THAT IS CURRENTLY BEING INTERPRETED.  WHILE WE ARE IN
                   184: ; THIS LOOP OR WHILE WE ARE INTERPRETING A DIRECT COMMAND
                   185: ; (SEE NEXT SECTION). "CURRNT" SHOULD POINT TO A 0.
                   186: ;
00ba 31 00 c0      187: RSTART: LXI  SP,STACK
00bd cd 0e 00      188: ST1:    CALL CRLF                       ;AND JUMP TO HERE
00c0 11 ab 00      189:         LXI  D,OK                       ;DE->STRING
00c3 97            190:         SUB  A                          ;A=0
00c4 cd 58 05      191:         CALL PRTSTG                     ;PRINT STRING UNTIL CR
00c7 21 ce 00      192:         LXI  H,ST2+1                    ;LITERAL 0
00ca 22 01 20      193:         SHLD CURRNT                     ;CURRENT->LINE # = 0
00cd 21 00 00      194: ST2:    LXI  H,0
00d0 22 09 20      195:         SHLD LOPVAR
00d3 22 03 20      196:         SHLD STKGOS
00d6 3e 3e         197: ST3:    MVI  A,3EH                      ;PROMPT '>' AND
00d8 cd f2 04      198:         CALL GETLN                      ;READ A LINE
00db d5            199:         PUSH D                          ;DE->END OF LINE
00dc 11 be 3f      200:         LXI  D,BUFFER                   ;DE->BEGINNING OF LINE
00df cd 77 00      201:         CALL TSTNUM                     ;TEST IF IT IS A NUMBER
00e2 ef            202:         RST  5
00e3 7c            203:         MOV  A,H                        ;HL=VALUE OF THE # OR
00e4 b5            204:         ORA  L                          ;0 IF NO # WAS FOUND
00e5 c1            205:         POP  B                          ;BC->END OF LINE
00e6 ca 24 07      206:         JZ   DIRECT
00e9 1b            207:         DCX  D                          ;BACKUP DE AND SAVE
00ea 7c            208:         MOV  A,H                        ;VALUE OF LINE # THERE
00eb 12            209:         STAX D
00ec 1b            210:         DCX  D
00ed 7d            211:         MOV  A,L
00ee 12            212:         STAX D
00ef c5            213:         PUSH B                          ;BC,DE->BEGIN, END
00f0 d5            214:         PUSH D
00f1 79            215:         MOV  A,C
00f2 93            216:         SUB  E
00f3 f5            217:         PUSH PSW                        ;A=# OF BYTES IN LINE
00f4 cd 30 05      218:         CALL FNDLN                      ;FIND THIS LINE IN SAVE
00f7 d5            219:         PUSH D                          ;AREA, DE->SAVE AREA
00f8 c2 0b 01      220:         JNZ  ST4                        ;NZ:NOT FOUND, INSERT
00fb d5            221:         PUSH D                          ;Z:FOUND, DELETE IT
00fc cd 4c 05      222:         CALL FNDNXT                     ;FIND NEXT LINE
                   223:                                         ;DE->NEXT LINE
00ff c1            224:         POP  B                          ;BC->LINE TO BE DELETED
0100 2a 15 20      225:         LHLD TXTUNF                     ;HL->UNFILLED SAVE AREA
0103 cd dd 05      226:         CALL MVUP                       ;MOVE UP TO DELETE
0106 60            227:         MOV  H,B                        ;TXTUNF->UNFILLED AREA
0107 69            228:         MOV  L,C
0108 22 15 20      229:         SHLD TXTUNF                     ;UPDATE
010b c1            230: ST4:    POP  B                          ;GET READY TO INSERT
010c 2a 15 20      231:         LHLD TXTUNF                     ;BUT FIRST CHECK IF
010f f1            232:         POP  PSW                        ;THE LENGTH OF NEW LINE
0110 e5            233:         PUSH H                          ;IS 3 (LINE # AND CR)
0111 fe 03         234:         CPI  3                          ;THEN DO NOT INSERT
0113 ca ba 00      235:         JZ   RSTART                     ;MUST CLEAR THE STACK
0116 85            236:         ADD  L                          ;COMPUTE NEW TXTUNF
0117 6f            237:         MOV  L,A
0118 3e 00         238:         MVI  A,0
011a 8c            239:         ADC  H
011b 67            240:         MOV  H,A                        ;HL->NEW UNFILLED AREA
011c 11 87 3f      241:         LXI  D,TXTEND                   ;CHECK TO SEE IF THERE
011f e7            242:         RST  4                          ;IS ENOUGH SPACE
0120 d2 eb 04      243:         JNC  QSORRY                     ;SORRY, NO ROOM FOR IT
0123 22 15 20      244:         SHLD TXTUNF                     ;OK, UPDATE TXTUNF
0126 d1            245:         POP  D                          ;DE->OLD UNFILLED AREA
0127 cd e6 05      246:         CALL MVDOWN
012a d1            247:         POP  D                          ;DE->BEGIN, HL->END
012b e1            248:         POP  H
012c cd dd 05      249:         CALL MVUP                       ;MOVE NEW LINE TO SAVE
012f c3 d6 00      250:         JMP  ST3                        ;AREA
                   251: ;
                   252: ;*************************************************************
                   253: ;
                   254: ; WHAT FOLLOWS IS THE CODE TO EXECUTE DIRECT AND STATEMENT
                   255: ; COMMANDS.  CONTROL IS TRANSFERED TO THESE POINTS VIA THE
                   256: ; COMMAND TABLE LOOKUP CODE OF 'DIRECT' AND 'EXEC' IN LAST
                   257: ; SECTION.  AFTER THE COMMAND IS EXECUTED, CONTROL IS
                   258: ; TRANSFERED TO OTHERS SECTIONS AS FOLLOWS:
                   259: ;
                   260: ; FOR 'LIST', 'NEW', AND 'STOP': GO BACK TO 'RSTART'
                   261: ; FOR 'RUN': GO EXECUTE THE FIRST STORED LINE IF ANY, ELSE
                   262: ; GO BACK TO 'RSTART'.
                   263: ; FOR 'GOTO' AND 'GOSUB': GO EXECUTE THE TARGET LINE.
                   264: ; FOR 'RETURN' AND 'NEXT': GO BACK TO SAVED RETURN LINE.
                   265: ; FOR ALL OTHERS: IF 'CURRENT' -> 0, GO TO 'RSTART', ELSE
                   266: ; GO EXECUTE NEXT COMMAND.  (THIS IS DONE IN 'FINISH'.)
                   267: ;*************************************************************
                   268: ;
                   269: ; *** NEW *** STOP *** RUN (& FRIENDS) *** & GOTO ***
                   270: ;
                   271: ; 'NEW(CR)' SETS 'TXTUNF' TO POINT TO 'TXTBGN'
                   272: ;
                   273: ; 'STOP(CR)' GOES BACK TO 'RSTART'
                   274: ;
                   275: ; 'RUN(CR)' FINDS THE FIRST STORED LINE, STORE ITS ADDRESS (IN
                   276: ; 'CURRENT'), AND START EXECUTE IT.  NOTE THAT ONLY THOSE
                   277: ; COMMANDS IN TAB2 ARE LEGAL FOR STORED PROGRAM.
                   278: ;
                   279: ; THERE ARE 3 MORE ENTRIES IN 'RUN':
                   280: ; 'RUNNXL' FINDS NEXT LINE, STORES ITS ADDR. AND EXECUTES IT.
                   281: ; 'RUNTSL' STORES THE ADDRESS OF THIS LINE AND EXECUTES IT.
                   282: ; 'RUNSML' CONTINUES THE EXECUTION ON SAME LINE.
                   283: ;
                   284: ; 'GOTO EXPR(CR)' EVALUATES THE EXPRESSION, FIND THE TARGET
                   285: ; LINE, AND JUMP TO 'RUNTSL' TO DO IT.
                   286: ;
0132 cd ba 04      287: NEW:    CALL ENDCHK                     ;*** NEW(CR) ***
0135 21 17 20      288:         LXI  H,TXTBGN
0138 22 15 20      289:         SHLD TXTUNF
                   290: ;
013b cd ba 04      291: STOP:   CALL ENDCHK                     ;*** STOP(CR) ***
013e c3 ba 00      292:         JMP  RSTART
                   293: ;
0141 cd ba 04      294: RUN:    CALL ENDCHK                     ;*** RUN(CR) ***
0144 11 17 20      295:         LXI  D,TXTBGN                   ;FIRST SAVED LINE
                   296: ;
0147 21 00 00      297: RUNNXL: LXI  H,0                        ;*** RUNNXL ***
014a cd 38 05      298:         CALL FNDLP                      ;FIND WHATEVER LINE #
014d da ba 00      299:         JC   RSTART                     ;C:PASSED TXTUNF, QUIT
                   300: ;
0150 eb            301: RUNTSL: XCHG                            ;*** RUNTSL ***
0151 22 01 20      302:         SHLD CURRNT                     ;SET 'CURRENT'->LINE #
0154 eb            303:         XCHG
0155 13            304:         INX  D                          ;BUMP PASS LINE #
0156 13            305:         INX  D
                   306: ;
0157 cd 6e 06      307: RUNSML: CALL CHKIO                      ;*** RUNSML ***
(06a9)             308: TAB2M1  EQU  TAB2-1
015a 21 a9 06      309:         LXI  H,TAB2M1                   ;FIND COMMAND IN TAB2
015d c3 27 07      310:         JMP  EXEC                       ;AND EXECUTE IT
                   311: ;
0160 df            312: GOTO:   RST  3                          ;*** GOTO EXPR ***
0161 d5            313:         PUSH D                          ;SAVE FOR ERROR ROUTINE
0162 cd ba 04      314:         CALL ENDCHK                     ;MUST FIND A CR
0165 cd 30 05      315:         CALL FNDLN                      ;FIND THE TARGET LINE
0168 c2 a0 00      316:         JNZ  AHOW                       ;NO SUCH LINE #
016b f1            317:         POP  PSW                        ;CLEAR THE PUSH DE
016c c3 50 01      318:         JMP  RUNTSL                     ;GO DO IT
                   319: ;
                   320: ;*************************************************************
                   321: ;
                   322: ; *** LIST *** & PRINT ***
                   323: ;
                   324: ; LIST HAS TWO FORMS:
                   325: ; 'LIST(CR)' LISTS ALL SAVED LINES
                   326: ; 'LIST #(CR)' START LIST AT THIS LINE #
                   327: ; YOU CAN STOP THE LISTING BY CONTROL C KEY
                   328: ;
                   329: ; PRINT COMMAND IS 'PRINT ....;' OR 'PRINT ....(CR)'
                   330: ; WHERE '....' IS A LIST OF EXPRESIONS, FORMATS, BACK-
                   331: ; ARROWS, AND STRINGS.  THESE ITEMS ARE SEPERATED BY COMMAS.
                   332: ;
                   333: ; A FORMAT IS A POUND SIGN FOLLOWED BY A NUMBER.  IT CONTROLS
                   334: ; THE NUMBER OF SPACES THE VALUE OF A EXPRESION IS GOING TO
                   335: ; BE PRINTED.  IT STAYS EFFECTIVE FOR THE REST OF THE PRINT
                   336: ; COMMAND UNLESS CHANGED BY ANOTHER FORMAT.  IF NO FORMAT IS
                   337: ; SPECIFIED, 6 POSITIONS WILL BE USED.
                   338: ;
                   339: ; A STRING IS QUOTED IN A PAIR OF SINGLE QUOTES OR A PAIR OF
                   340: ; DOUBLE QUOTES.
                   341: ;
                   342: ; A BACK-ARROW MEANS GENERATE A (CR) WITHOUT (LF)
                   343: ;
                   344: ; A (CRLF) IS GENERATED AFTER THE ENTIRE LIST HAS BEEN
                   345: ; PRINTED OR IF THE LIST IS A NULL LIST.  HOWEVER IF THE LIST
                   346: ; ENDED WITH A COMMA, NO (CRLF) IS GENERATED.
                   347: ;
016f cd 77 00      348: LIST:   CALL TSTNUM                     ;TEST IF THERE IS A #
0172 cd ba 04      349:         CALL ENDCHK                     ;IF NO # WE GET A 0
0175 cd 30 05      350:         CALL FNDLN                      ;FIND THIS OR NEXT LINE
0178 da ba 00      351: LS1:    JC   RSTART                     ;C:PASSED TXTUNF
017b cd ca 05      352:         CALL PRTLN                      ;PRINT THE LINE
017e cd 6e 06      353:         CALL CHKIO                      ;STOP IF HIT CONTROL-C
0181 cd 38 05      354:         CALL FNDLP                      ;FIND NEXT LINE
0184 c3 78 01      355:         JMP  LS1                        ;AND LOOP BACK
                   356: ;
0187 0e 06         357: PRINT:  MVI  C,6                        ;C = # OF SPACES
0189 cf            358:         RST  1                          ;IF NULL LIST & ";"
018a 3b            359:         DB   3BH
018b 06            360:         DB   PR2-$-1
018c cd 0e 00      361:         CALL CRLF                       ;GIVE CR-LF AND
018f c3 57 01      362:         JMP  RUNSML                     ;CONTINUE SAME LINE
0192 cf            363: PR2:    RST  1                          ;IF NULL LIST (CR)
0193 0d            364:         DB   CR
0194 06            365:         DB   PR0-$-1
0195 cd 0e 00      366:         CALL CRLF                       ;ALSO GIVE CR-LF AND
0198 c3 47 01      367:         JMP  RUNNXL                     ;GO TO NEXT LINE
019b cf            368: PR0:    RST  1                          ;ELSE IS IT FORMAT?
019c 23            369:         DB   '#'
019d 05            370:         DB   PR1-$-1
019e df            371:         RST  3                          ;YES, EVALUATE EXPR.
019f 4d            372:         MOV  C,L                        ;AND SAVE IT IN C
01a0 c3 a9 01      373:         JMP  PR3                        ;LOOK FOR MORE TO PRINT
01a3 cd 64 05      374: PR1:    CALL QTSTG                      ;OR IS IT A STRING?
01a6 c3 b6 01      375:         JMP  PR8                        ;IF NOT, MUST BE EXPR.
01a9 cf            376: PR3:    RST  1                          ;IF ",", GO FIND NEXT
01aa 2c            377:         DB   ','
01ab 06            378:         DB   PR6-$-1
01ac cd ab 04      379:         CALL FIN                        ;IN THE LIST.
01af c3 9b 01      380:         JMP  PR0                        ;LIST CONTINUES
01b2 cd 0e 00      381: PR6:    CALL CRLF                       ;LIST ENDS
01b5 f7            382:         RST  6
01b6 df            383: PR8:    RST  3                          ;EVALUATE THE EXPR
01b7 c5            384:         PUSH B
01b8 cd 8a 05      385:         CALL PRTNUM                     ;PRINT THE VALUE
01bb c1            386:         POP  B
01bc c3 a9 01      387:         JMP  PR3                        ;MORE TO PRINT?
                   388: ;
                   389: ;*************************************************************
                   390: ;
                   391: ; *** GOSUB *** & RETURN ***
                   392: ;
                   393: ; 'GOSUB EXPR;' OR 'GOSUB EXPR (CR)' IS LIKE THE 'GOTO'
                   394: ; COMMAND, EXCEPT THAT THE CURRENT TEXT POINTER, STACK POINTER
                   395: ; ETC. ARE SAVE SO THAT EXECUTION CAN BE CONTINUED AFTER THE
                   396: ; SUBROUTINE 'RETURN'.  IN ORDER THAT 'GOSUB' CAN BE NESTED
                   397: ; (AND EVEN RECURSIVE), THE SAVE AREA MUST BE STACKED.
                   398: ; THE STACK POINTER IS SAVED IN 'STKGOS', THE OLD 'STKGOS' IS
                   399: ; SAVED IN THE STACK.  IF WE ARE IN THE MAIN ROUTINE, 'STKGOS'
                   400: ; IS ZERO (THIS WAS DONE BY THE "MAIN" SECTION OF THE CODE),
                   401: ; BUT WE STILL SAVE IT AS A FLAG FOR NO FURTHER 'RETURN'S.
                   402: ;
                   403: ; 'RETURN(CR)' UNDOS EVERYTHING THAT 'GOSUB' DID, AND THUS
                   404: ; RETURN THE EXECUTION TO THE COMMAND AFTER THE MOST RECENT
                   405: ; 'GOSUB'.  IF 'STKGOS' IS ZERO, IT INDICATES THAT WE
                   406: ; NEVER HAD A 'GOSUB' AND IS THUS AN ERROR.
                   407: ;
01bf cd 11 06      408: GOSUB:  CALL PUSHA                      ;SAVE THE CURRENT "FOR"
01c2 df            409:         RST  3                          ;PARAMETERS
01c3 d5            410:         PUSH D                          ;AND TEXT POINTER
01c4 cd 30 05      411:         CALL FNDLN                      ;FIND THE TARGET LINE
01c7 c2 a0 00      412:         JNZ  AHOW                       ;NOT THERE. SAY "HOW?"
01ca 2a 01 20      413:         LHLD CURRNT                     ;FOUND IT, SAVE OLD
01cd e5            414:         PUSH H                          ;'CURRNT' OLD 'STKGOS'
01ce 2a 03 20      415:         LHLD STKGOS
01d1 e5            416:         PUSH H
01d2 21 00 00      417:         LXI  H,0                        ;AND LOAD NEW ONES
01d5 22 09 20      418:         SHLD LOPVAR
01d8 39            419:         DAD  SP
01d9 22 03 20      420:         SHLD STKGOS
01dc c3 50 01      421:         JMP  RUNTSL                     ;THEN RUN THAT LINE
01df cd ba 04      422: RETURN: CALL ENDCHK                     ;THERE MUST BE A CR
01e2 2a 03 20      423:         LHLD STKGOS                     ;OLD STACK POINTER
01e5 7c            424:         MOV  A,H                        ;0 MEANS NOT EXIST
01e6 b5            425:         ORA  L
01e7 ca be 04      426:         JZ   QWHAT                      ;SO, WE SAY: "WHAT?"
01ea f9            427:         SPHL                            ;ELSE, RESTORE IT
01eb e1            428:         POP  H
01ec 22 03 20      429:         SHLD STKGOS                     ;AND THE OLD 'STKGOS'
01ef e1            430:         POP  H
01f0 22 01 20      431:         SHLD CURRNT                     ;AND THE OLD 'CURRNT'
01f3 d1            432:         POP  D                          ;OLD TEXT POINTER
01f4 cd f5 05      433:         CALL POPA                       ;OLD "FOR" PARAMETERS
01f7 f7            434:         RST  6                          ;AND WE ARE BACK HOME
                   435: ;
                   436: ;*************************************************************
                   437: ;
                   438: ; *** FOR *** & NEXT ***
                   439: ;
                   440: ; 'FOR' HAS TWO FORMS:
                   441: ; 'FOR VAR=EXP1 TO EXP2 STEP EXP3' AND 'FOR VAR=EXP1 TO EXP2'
                   442: ; THE SECOND FORM MEANS THE SAME THING AS THE FIRST FORM WITH
                   443: ; EXP3=1.  (I.E., WITH A STEP OF +1.)
                   444: ; TBI WILL FIND THE VARIABLE VAR, AND SET ITS VALUE TO THE
                   445: ; CURRENT VALUE OF EXP1.  IT ALSO EVALUATES EXP2 AND EXP3
                   446: ; AND SAVE ALL THESE TOGETHER WITH THE TEXT POINTER ETC. IN
                   447: ; THE 'FOR' SAVE AREA, WHICH CONSISTS OF 'LOPVAR', 'LOPINC',
                   448: ; 'LOPLMT', 'LOPLN', AND 'LOPPT'.  IF THERE IS ALREADY SOME-
                   449: ; THING IN THE SAVE AREA (THIS IS INDICATED BY A NON-ZERO
                   450: ; 'LOPVAR'), THEN THE OLD SAVE AREA IS SAVED IN THE STACK
                   451: ; BEFORE THE NEW ONE OVERWRITES IT.
                   452: ; TBI WILL THEN DIG IN THE STACK AND FIND OUT IF THIS SAME
                   453: ; VARIABLE WAS USED IN ANOTHER CURRENTLY ACTIVE 'FOR' LOOP.
                   454: ; IF THAT IS THE CASE, THEN THE OLD 'FOR' LOOP IS DEACTIVATED.
                   455: ; (PURGED FROM THE STACK..)
                   456: ;
                   457: ; 'NEXT VAR' SERVES AS THE LOGICAL (NOT NECESSARILLY PHYSICAL)
                   458: ; END OF THE 'FOR' LOOP.  THE CONTROL VARIABLE VAR. IS CHECKED
                   459: ; WITH THE 'LOPVAR'.  IF THEY ARE NOT THE SAME, TBI DIGS IN
                   460: ; THE STACK TO FIND THE RIGHT ONE AND PURGES ALL THOSE THAT
                   461: ; DID NOT MATCH.  EITHER WAY, TBI THEN ADDS THE 'STEP' TO
                   462: ; THAT VARIABLE AND CHECK THE RESULT WITH THE LIMIT.  IF IT
                   463: ; IS WITHIN THE LIMIT, CONTROL LOOPS BACK TO THE COMMAND
                   464: ; FOLLOWING THE 'FOR'.  IF OUTSIDE THE LIMIT, THE SAVE AREA
                   465: ; IS PURGED AND EXECUTION CONTINUES.
                   466: ;
01f8 cd 11 06      467: FOR:    CALL PUSHA                      ;SAVE THE OLD SAVE AREA
01fb cd 98 04      468:         CALL SETVAL                     ;SET THE CONTROL VAR.
01fe 2b            469:         DCX  H                          ;HL IS ITS ADDRESS
01ff 22 09 20      470:         SHLD LOPVAR                     ;SAVE THAT
0202 21 ff 06      471:         LXI  H,TAB5-1                   ;USE 'EXEC' TO LOOK
0205 c3 27 07      472:         JMP  EXEC                       ;FOR THE WORD 'TO'
0208 df            473: FR1:    RST  3                          ;EVALUATE THE LIMIT
0209 22 0d 20      474:         SHLD LOPLMT                     ;SAVE THAT
020c 21 05 07      475:         LXI  H,TAB6-1                   ;USE 'EXEC' TO LOOK
020f c3 27 07      476:         JMP EXEC                        ;FOR THE WORD 'STEP'
0212 df            477: FR2:    RST  3                          ;FOUND IT, GET STEP
0213 c3 15 02      478:         JMP  FR4
0216 21 01 00      479: FR3:    LXI  H,1H                       ;NOT FOUND, SET TO 1
0219 22 0b 20      480: FR4:    SHLD LOPINC                     ;SAVE THAT TOO
021c 2a 01 20      481: FR5:    LHLD CURRNT                     ;SAVE CURRENT LINE #
021f 22 0f 20      482:         SHLD LOPLN
0222 eb            483:         XCHG                            ;AND TEXT POINTER
0223 22 11 20      484:         SHLD LOPPT
0226 01 0a 00      485:         LXI  B,0AH                      ;DIG INTO STACK TO
0229 2a 09 20      486:         LHLD LOPVAR                     ;FIND 'LOPVAR'
022c eb            487:         XCHG
022d 60            488:         MOV  H,B
022e 68            489:         MOV  L,B                        ;HL=0 NOW
022f 39            490:         DAD  SP                         ;HERE IS THE STACK
0230 3e            491:         DB   3EH
0231 09            492: FR7:    DAD  B                          ;EACH LEVEL IS 10 DEEP
0232 7e            493:         MOV  A,M                        ;GET THAT OLD 'LOPVAR'
0233 23            494:         INX  H
0234 b6            495:         ORA  M
0235 ca 4e 02      496:         JZ   FR8                        ;0 SAYS NO MORE IN IT
0238 7e            497:         MOV  A,M
0239 2b            498:         DCX  H
023a ba            499:         CMP  D                          ;SAME AS THIS ONE?
023b c2 2d 02      500:         JNZ  FR7
023e 7e            501:         MOV  A,M                        ;THE OTHER HALF?
023f bb            502:         CMP  E
0240 c2 2d 02      503:         JNZ  FR7
0243 eb            504:         XCHG                            ;YES, FOUND ONE
0244 21 00 00      505:         LXI  H,0H
0247 39            506:         DAD  SP                         ;TRY TO MOVE SP
0248 44            507:         MOV  B,H
0249 4d            508:         MOV  C,L
024a 21 0a 00      509:         LXI  H,0AH
024d 19            510:         DAD  D
024e cd e6 05      511:         CALL MVDOWN                     ;AND PURGE 10 WORDS
0251 f9            512:         SPHL                            ;IN THE STACK
0252 2a 11 20      513: FR8:    LHLD LOPPT                      ;JOB DONE, RESTORE DE
0255 eb            514:         XCHG
0256 f7            515:         RST  6                          ;AND CONTINUE
                   516: ;
0257 ff            517: NEXT:   RST  7                          ;GET ADDRESS OF VAR.
0258 da be 04      518:         JC   QWHAT                      ;NO VARIABLE, "WHAT?"
025b 22 05 20      519:         SHLD VARNXT                     ;YES, SAVE IT
025e d5            520: NX0:    PUSH D                          ;SAVE TEXT POINTER
025f eb            521:         XCHG
0260 2a 09 20      522:         LHLD LOPVAR                     ;GET VAR. IN 'FOR'
0263 7c            523:         MOV  A,H
0264 b5            524:         ORA  L                          ;0 SAYS NEVER HAD ONE
0265 ca bf 04      525:         JZ   AWHAT                      ;SO WE ASK: "WHAT?"
0268 e7            526:         RST  4                          ;ELSE WE CHECK THEM
0269 ca 72 02      527:         JZ   NX3                        ;OK, THEY AGREE
026c d1            528:         POP  D                          ;NO, LET'S SEE
026d cd f5 05      529:         CALL POPA                       ;PURGE CURRENT LOOP
0270 2a 05 20      530:         LHLD VARNXT                     ;AND POP ONE LEVEL
0273 c3 5a 02      531:         JMP  NX0                        ;GO CHECK AGAIN
0276 5e            532: NX3:    MOV  E,M                        ;COME HERE WHEN AGREED
0277 23            533:         INX  H
0278 56            534:         MOV  D,M                        ;DE=VALUE OF VAR.
0279 2a 0b 20      535:         LHLD LOPINC
027c e5            536:         PUSH H
027d 7c            537:         MOV  A,H
027e aa            538:         XRA  D
027f 7a            539:         MOV  A,D
0280 19            540:         DAD  D                          ;ADD ONE STEP
0281 fa 84 02      541:         JM   NX4
0284 ac            542:         XRA  H
0285 fa a6 02      543:         JM   NX5
0288 eb            544: NX4:    XCHG
0289 2a 09 20      545:         LHLD LOPVAR                     ;PUT IT BACK
028c 73            546:         MOV  M,E
028d 23            547:         INX  H
028e 72            548:         MOV  M,D
028f 2a 0d 20      549:         LHLD LOPLMT                     ;HL->LIMIT
0292 f1            550:         POP  PSW                        ;OLD HL
0293 b7            551:         ORA  A
0294 f2 94 02      552:         JP   NX1                        ;STEP > 0
0297 eb            553:         XCHG                            ;STEP < 0
0298 cd 90 04      554: NX1:    CALL CKHLDE                     ;COMPARE WITH LIMIT
029b d1            555:         POP  D                          ;RESTORE TEXT POINTER
029c da a8 02      556:         JC   NX2                        ;OUTSIDE LIMIT
029f 2a 0f 20      557:         LHLD LOPLN                      ;WITHIN LIMIT, GO
02a2 22 01 20      558:         SHLD CURRNT                     ;BACK TO THE SAVED
02a5 2a 11 20      559:         LHLD LOPPT                      ;'CURRNT' AND TEXT
02a8 eb            560:         XCHG                            ;POINTER
02a9 f7            561:         RST  6
02aa e1            562: NX5:    POP  H
02ab d1            563:         POP  D
02ac cd f5 05      564: NX2:    CALL POPA                       ;PURGE THIS LOOP
02af f7            565:         RST  6
                   566: ;
                   567: ;*************************************************************
                   568: ;
                   569: ; *** REM *** IF *** INPUT *** & LET (& DEFLT) ***
                   570: ;
                   571: ; 'REM' CAN BE FOLLOWED BY ANYTHING AND IS IGNORED BY TBI.
                   572: ; TBI TREATS IT LIKE AN 'IF' WITH A FALSE CONDITION.
                   573: ;
                   574: ; 'IF' IS FOLLOWED BY AN EXPR. AS A CONDITION AND ONE OR MORE
                   575: ; COMMANDS (INCLUDING OTHER 'IF'S) SEPERATED BY SEMI-COLONS.
                   576: ; NOTE THAT THE WORD 'THEN' IS NOT USED.  TBI EVALUATES THE
                   577: ; EXPR. IF IT IS NON-ZERO, EXECUTION CONTINUES.  IF THE
                   578: ; EXPR. IS ZERO, THE COMMANDS THAT FOLLOWS ARE IGNORED AND
                   579: ; EXECUTION CONTINUES AT THE NEXT LINE.
                   580: ;
                   581: ; 'INPUT' COMMAND IS LIKE THE 'PRINT' COMMAND, AND IS FOLLOWED
                   582: ; BY A LIST OF ITEMS.  IF THE ITEM IS A STRING IN SINGLE OR
                   583: ; DOUBLE QUOTES, OR IS A BACK-ARROW, IT HAS THE SAME EFFECT AS
                   584: ; IN 'PRINT'.  IF AN ITEM IS A VARIABLE, THIS VARIABLE NAME IS
                   585: ; PRINTED OUT FOLLOWED BY A COLON.  THEN TBI WAITS FOR AN
                   586: ; EXPR. TO BE TYPED IN.  THE VARIABLE IS THEN SET TO THE
                   587: ; VALUE OF THIS EXPR.  IF THE VARIABLE IS PROCEDED BY A STRING
                   588: ; (AGAIN IN SINGLE OR DOUBLE QUOTES), THE STRING WILL BE
                   589: ; PRINTED FOLLOWED BY A COLON.  TBI THEN WAITS FOR INPUT EXPR.
                   590: ; AND SET THE VARIABLE TO THE VALUE OF THE EXPR.
                   591: ;
                   592: ; IF THE INPUT EXPR. IS INVALID, TBI WILL PRINT "WHAT?",
                   593: ; "HOW?" OR "SORRY" AND REPRINT THE PROMPT AND REDO THE INPUT.
                   594: ; THE EXECUTION WILL NOT TERMINATE UNLESS YOU TYPE CONTROL-C.
                   595: ; THIS IS HANDLED IN 'INPERR'.
                   596: ;
                   597: ; 'LET' IS FOLLOWED BY A LIST OF ITEMS SEPERATED BY COMMAS.
                   598: ; EACH ITEM CONSISTS OF A VARIABLE, AN EQUAL SIGN, AND AN EXPR.
                   599: ; TBI EVALUATES THE EXPR. AND SET THE VARIABLE TO THAT VALUE.
                   600: ; TBI WILL ALSO HANDLE 'LET' COMMAND WITHOUT THE WORD 'LET'.
                   601: ; THIS IS DONE BY 'DEFLT'.
                   602: ;
02b0 21 00 00      603: REM:    LXI  H,0H                       ;*** REM ***
02b3 3e            604:         DB   3EH                        ;THIS IS LIKE 'IF 0'
                   605: ;
02b4 df            606: IFF:    RST  3                          ;*** IF ***
02b5 7c            607:         MOV  A,H                        ;IS THE EXPR.=0?
02b6 b5            608:         ORA  L
02b7 c2 57 01      609:         JNZ  RUNSML                     ;NO, CONTINUE
02ba cd 4e 05      610:         CALL FNDSKP                     ;YES, SKIP REST OF LINE
02bd d2 50 01      611:         JNC  RUNTSL                     ;AND RUN THE NEXT LINE
02c0 c3 ba 00      612:         JMP  RSTART                     ;IF NO NEXT, RE-START
                   613: ;
02c3 2a 07 20      614: INPERR: LHLD STKINP                     ;*** INPERR ***
02c6 f9            615:         SPHL                            ;RESTORE OLD SP
02c7 e1            616:         POP  H                          ;AND OLD 'CURRNT'
02c8 22 01 20      617:         SHLD CURRNT
02cb d1            618:         POP  D                          ;AND OLD TEXT POINTER
02cc d1            619:         POP  D                          ;REDO INPUT
                   620: ;
                   621: INPUT:                                  ;*** INPUT ***
02cd d5            622: IP1:    PUSH D                          ;SAVE IN CASE OF ERROR
02ce cd 64 05      623:         CALL QTSTG                      ;IS NEXT ITEM A STRING?
02d1 c3 d7 02      624:         JMP  IP2                        ;NO
02d4 ff            625:         RST  7                          ;YES, BUT FOLLOWED BY A
02d5 da 11 03      626:         JC   IP4                        ;VARIABLE?   NO.
02d8 c3 e7 02      627:         JMP  IP3                        ;YES.  INPUT VARIABLE
02db d5            628: IP2:    PUSH D                          ;SAVE FOR 'PRTSTG'
02dc ff            629:         RST  7                          ;MUST BE VARIABLE NOW
02dd da be 04      630:         JC   QWHAT                      ;"WHAT?" IT IS NOT?
02e0 1a            631:         LDAX D                          ;GET READY FOR 'PRTSTR'
02e1 4f            632:         MOV  C,A
02e2 97            633:         SUB  A
02e3 12            634:         STAX D
02e4 d1            635:         POP  D
02e5 cd 58 05      636:         CALL PRTSTG                     ;PRINT STRING AS PROMPT
02e8 79            637:         MOV  A,C                        ;RESTORE TEXT
02e9 1b            638:         DCX  D
02ea 12            639:         STAX D
02eb d5            640: IP3:    PUSH D                          ;SAVE TEXT POINTER
02ec eb            641:         XCHG
02ed 2a 01 20      642:         LHLD CURRNT                     ;ALSO SAVE 'CURRNT'
02f0 e5            643:         PUSH H
02f1 21 c9 02      644:         LXI  H,IP1                      ;A NEGATIVE NUMBER
02f4 22 01 20      645:         SHLD CURRNT                     ;AS A FLAG
02f7 21 00 00      646:         LXI  H,0H                       ;SAVE SP TOO
02fa 39            647:         DAD  SP
02fb 22 07 20      648:         SHLD STKINP
02fe d5            649:         PUSH D                          ;OLD HL
02ff 3e 3a         650:         MVI  A,3AH                      ;PRINT THIS TOO
0301 cd f2 04      651:         CALL GETLN                      ;AND GET A LINE
0304 11 be 3f      652:         LXI  D,BUFFER                   ;POINTS TO BUFFER
0307 df            653:         RST  3                          ;EVALUATE INPUT
0308 00            654:         NOP                             ;CAN BE 'CALL ENDCHK'
0309 00            655:         NOP
030a 00            656:         NOP
030b d1            657:         POP  D                          ;OK, GET OLD HL
030c eb            658:         XCHG
030d 73            659:         MOV  M,E                        ;SAVE VALUE IN VAR.
030e 23            660:         INX  H
030f 72            661:         MOV  M,D
0310 e1            662:         POP  H                          ;GET OLD 'CURRNT'
0311 22 01 20      663:         SHLD CURRNT
0314 d1            664:         POP  D                          ;AND OLD TEXT POINTER
0315 f1            665: IP4:    POP  PSW                        ;PURGE JUNK IN STACK
0316 cf            666:         RST  1                          ;IS NEXT CH. ','?
0317 2c            667:         DB   ','
                   668:         DB   IP5-$-1
ERROR - Ä?L¸}·Ø†2L

0318 c3 c9 02      669:         JMP  IP1                        ;YES, MORE ITEMS.
031b f7            670: IP5:    RST  6
                   671: ;
031c 1a            672: DEFLT:  LDAX D                          ;***  DEFLT ***
031d fe 0d         673:         CPI  CR                         ;EMPTY LINE IS OK
031f ca 28 03      674:         JZ   LT1                        ;ELSE IT IS 'LET'
                   675: ;
0322 cd 98 04      676: LET:    CALL SETVAL                     ;*** LET ***
0325 cf            677:         RST  1                          ;SET VALUE TO VAR.
0326 2c            678:         DB   ','
0327 00            679:         DB   LT1-$-1
0328 c3 1f 03      680:         JMP  LET                        ;ITEM BY ITEM
032b f7            681: LT1:    RST  6                          ;UNTIL FINISH
                   682: ;
                   683: ;*************************************************************
                   684: ;
                   685: ; *** EXPR ***
                   686: ;
                   687: ; 'EXPR' EVALUATES ARITHMETICAL OR LOGICAL EXPRESSIONS.
                   688: ; <EXPR>::<EXPR2>
                   689: ;         <EXPR2><REL.OP.><EXPR2>
                   690: ; WHERE <REL.OP.> IS ONE OF THE OPERATORS IN TAB8 AND THE
                   691: ; RESULT OF THESE OPERATIONS IS 1 IF TRUE AND 0 IF FALSE.
                   692: ; <EXPR2>::=(+ OR -)<EXPR3>(+ OR -<EXPR3>)(....)
                   693: ; WHERE () ARE OPTIONAL AND (....) ARE OPTIONAL REPEATS.
                   694: ; <EXPR3>::=<EXPR4>(* OR /><EXPR4>)(....)
                   695: ; <EXPR4>::=<VARIABLE>
                   696: ;           <FUNCTION>
                   697: ;           (<EXPR>)
                   698: ; <EXPR> IS RECURSIVE SO THAT VARIABLE '@' CAN HAVE AN <EXPR>
                   699: ; AS INDEX, FUNCTIONS CAN HAVE AN <EXPR> AS ARGUMENTS, AND
                   700: ; <EXPR4> CAN BE AN <EXPR> IN PARANTHESE.
                   701: ;
                   702: ;EXPR:  CALL EXPR2                      ;THIS IS AT LOC. 18
                   703: ;       PUSH H                          ;SAVE <EXPR2> VALUE
032c 21 0d 07      704: EXPR1:  LXI  H,TAB8-1                   ;LOOKUP REL.OP.
032f c3 27 07      705:         JMP  EXEC                       ;GO DO IT
0332 cd 56 03      706: XP11:   CALL XP18                       ;REL.OP.">="
0335 d8            707:         RC                              ;NO, RETURN HL=0
0336 6f            708:         MOV  L,A                        ;YES, RETURN HL=1
0337 c9            709:         RET
0338 cd 56 03      710: XP12:   CALL XP18                       ;REL.OP."#"
033b c8            711:         RZ                              ;FALSE, RETURN HL=0
033c 6f            712:         MOV  L,A                        ;TRUE, RETURN HL=1
033d c9            713:         RET
033e cd 56 03      714: XP13:   CALL XP18                       ;REL.OP.">"
0341 c8            715:         RZ                              ;FALSE
0342 d8            716:         RC                              ;ALSO FALSE, HL=0
0343 6f            717:         MOV  L,A                        ;TRUE, HL=1
0344 c9            718:         RET
0345 cd 56 03      719: XP14:   CALL XP18                       ;REL.OP."<="
0348 6f            720:         MOV  L,A                        ;SET HL=1
0349 c8            721:         RZ                              ;REL. TRUE, RETURN
034a d8            722:         RC
034b 6c            723:         MOV  L,H                        ;ELSE SET HL=0
034c c9            724:         RET
034d cd 56 03      725: XP15:   CALL XP18                       ;REL.OP."="
0350 c0            726:         RNZ                             ;FALSE, RETURN HL=0
0351 6f            727:         MOV  L,A                        ;ELSE SET HL=1
0352 c9            728:         RET
0353 cd 56 03      729: XP16:   CALL XP18                       ;REL.OP."<"
0356 d0            730:         RNC                             ;FALSE, RETURN HL=0
0357 6f            731:         MOV  L,A                        ;ELSE SET HL=1
0358 c9            732:         RET
0359 e1            733: XP17:   POP  H                          ;NOT .REL.OP
035a c9            734:         RET                             ;RETURN HL=<EXPR2>
035b 79            735: XP18:   MOV  A,C                        ;SUBROUTINE FOR ALL
035c e1            736:         POP  H                          ;REL.OP.'S
035d c1            737:         POP  B
035e e5            738:         PUSH H                          ;REVERSE TOP OF STACK
035f c5            739:         PUSH B
0360 4f            740:         MOV  C,A
0361 cd 6b 03      741:         CALL EXPR2                      ;GET 2ND <EXPR2>
0364 eb            742:         XCHG                            ;VALUE IN DE NOW
0365 e3            743:         XTHL                            ;1ST <EXPR2> IN HL
0366 cd 90 04      744:         CALL CKHLDE                     ;COMPARE 1ST WITH 2ND
0369 d1            745:         POP  D                          ;RESTORE TEXT POINTER
036a 21 00 00      746:         LXI  H,0H                       ;SET HL=0, A=1
036d 3e 01         747:         MVI  A,1
036f c9            748:         RET
                   749: ;
0370 cf            750: EXPR2:  RST  1                          ;NEGATIVE SIGN?
0371 2d            751:         DB   '-'
0372 01            752:         DB   XP21-$-1
0373 21 00 00      753:         LXI  H,0H                       ;YES, FAKE '0-'
0376 c3 95 03      754:         JMP  XP26                       ;TREAT LIKE SUBTRACT
0379 cf            755: XP21:   RST  1                          ;POSITIVE SIGN? IGNORE
037a 2b            756:         DB   '+'
037b fb            757:         DB   XP22-$-1
037c cd 9f 03      758: XP22:   CALL EXPR3                      ;1ST <EXPR3>
037f cf            759: XP23:   RST  1                          ;ADD?
0380 2b            760:         DB   '+'
0381 10            761:         DB   XP25-$-1
0382 e5            762:         PUSH H                          ;YES, SAVE VALUE
0383 cd 9f 03      763:         CALL EXPR3                      ;GET 2ND <EXPR3>
0386 eb            764: XP24:   XCHG                            ;2ND IN DE
0387 e3            765:         XTHL                            ;1ST IN HL
0388 7c            766:         MOV  A,H                        ;COMPARE SIGN
0389 aa            767:         XRA  D
038a 7a            768:         MOV  A,D
038b 19            769:         DAD  D
038c d1            770:         POP  D                          ;RESTORE TEXT POINTER
038d fa 7a 03      771:         JM   XP23                       ;1ST AND 2ND SIGN DIFFER
0390 ac            772:         XRA  H                          ;1ST AND 2ND SIGN EQUAL
0391 f2 7a 03      773:         JP   XP23                       ;SO IS RESULT
0394 c3 9f 00      774:         JMP  QHOW                       ;ELSE WE HAVE OVERFLOW
0397 cf            775: XP25:   RST  1                          ;SUBTRACT?
0398 2d            776:         DB   '-'
0399 7f            777:         DB   XP42-$-1
039a e5            778: XP26:   PUSH H                          ;YES, SAVE 1ST <EXPR3>
039b cd 9f 03      779:         CALL EXPR3                      ;GET 2ND <EXPR3>
039e cd 7e 04      780:         CALL CHGSGN                     ;NEGATE
03a1 c3 81 03      781:         JMP  XP24                       ;AND ADD THEM
                   782: ;
03a4 cd ff 03      783: EXPR3:  CALL EXPR4                      ;GET 1ST <EXPR4>
03a7 cf            784: XP31:   RST  1                          ;MULTIPLY?
03a8 2a            785:         DB   '*'
03a9 28            786:         DB   XP34-$-1
03aa e5            787:         PUSH H                          ;YES, SAVE 1ST
03ab cd ff 03      788:         CALL EXPR4                      ;AND GET 2ND <EXPR4>
03ae 06 00         789:         MVI  B,0H                       ;CLEAR B FOR SIGN
03b0 cd 7b 04      790:         CALL CHKSGN                     ;CHECK SIGN
03b3 e3            791:         XTHL                            ;1ST IN HL
03b4 cd 7b 04      792:         CALL CHKSGN                     ;CHECK SIGN OF 1ST
03b7 eb            793:         XCHG
03b8 e3            794:         XTHL
03b9 7c            795:         MOV  A,H                        ;IS HL > 255 ?
03ba b7            796:         ORA  A
03bb ca bf 03      797:         JZ   XP32                       ;NO
03be 7a            798:         MOV  A,D                        ;YES, HOW ABOUT DE
03bf b2            799:         ORA  D
03c0 eb            800:         XCHG                            ;PUT SMALLER IN HL
03c1 c2 a0 00      801:         JNZ  AHOW                       ;ALSO >, WILL OVERFLOW
03c4 7d            802: XP32:   MOV  A,L                        ;THIS IS DUMB
03c5 21 00 00      803:         LXI  H,0H                       ;CLEAR RESULT
03c8 b7            804:         ORA  A                          ;ADD AND COUNT
03c9 ca f1 03      805:         JZ   XP35
03cc 19            806: XP33:   DAD  D
03cd da a0 00      807:         JC   AHOW                       ;OVERFLOW
03d0 3d            808:         DCR  A
03d1 c2 c7 03      809:         JNZ  XP33
03d4 c3 f1 03      810:         JMP  XP35                       ;FINISHED
03d7 cf            811: XP34:   RST  1                          ;DIVIDE?
03d8 2f            812:         DB   '/'
03d9 3f            813:         DB   XP42-$-1
03da e5            814:         PUSH H                          ;YES, SAVE 1ST <EXPR4>
03db cd ff 03      815:         CALL EXPR4                      ;AND GET THE SECOND ONE
03de 06 00         816:         MVI  B,0H                       ;CLEAR B FOR SIGN
03e0 cd 7b 04      817:         CALL CHKSGN                     ;CHECK SIGN OF 2ND
03e3 e3            818:         XTHL                            ;GET 1ST IN HL
03e4 cd 7b 04      819:         CALL CHKSGN                     ;CHECK SIGN OF 1ST
03e7 eb            820:         XCHG
03e8 e3            821:         XTHL
03e9 eb            822:         XCHG
03ea 7a            823:         MOV  A,D                        ;DIVIDE BY 0?
03eb b3            824:         ORA  E
03ec ca a0 00      825:         JZ   AHOW                       ;SAY "HOW?"
03ef c5            826:         PUSH B                          ;ELSE SAVE SIGN
03f0 cd 5e 04      827:         CALL DIVIDE                     ;USE SUBROUTINE
03f3 60            828:         MOV  H,B                        ;RESULT IN HL NOW
03f4 69            829:         MOV  L,C
03f5 c1            830:         POP  B                          ;GET SIGN BACK
03f6 d1            831: XP35:   POP  D                          ;AND TEXT POINTER
03f7 7c            832:         MOV  A,H                        ;HL MUST BE +
03f8 b7            833:         ORA  A
03f9 fa 9f 00      834:         JM   QHOW                       ;ELSE IT IS OVERFLOW
03fc 78            835:         MOV  A,B
03fd b7            836:         ORA  A
03fe fc 7e 04      837:         CM   CHGSGN                     ;CHANGE SIGN IF NEEDED
0401 c3 a2 03      838:         JMP  XP31                       ;LOOK FOR MORE TERMS
                   839: ;
0404 21 ed 06      840: EXPR4:  LXI  H,TAB4-1                   ;FIND FUNCTION IN TAB4
0407 c3 27 07      841:         JMP  EXEC                       ;AND GO DO IT
040a ff            842: XP40:   RST  7                          ;NO, NOT A FUNCTION
040b da 0c 04      843:         JC   XP41                       ;NOR A VARIABLE
040e 7e            844:         MOV  A,M                        ;VARIABLE
040f 23            845:         INX  H
0410 66            846:         MOV  H,M                        ;VALUE IN HL
0411 6f            847:         MOV  L,A
0412 c9            848:         RET
0413 cd 77 00      849: XP41:   CALL TSTNUM                     ;OR IS IT A NUMBER
0416 78            850:         MOV  A,B                        ;# OF DIGIT
0417 b7            851:         ORA  A
0418 c0            852:         RNZ                             ;OK
0419 cf            853: PARN:   RST  1
041a 28            854:         DB   '('
041b fe            855:         DB   XP43-$-1
041c df            856:         RST  3                          ;"(EXPR)"
041d cf            857:         RST  1
041e 29            858:         DB   ')'
041f fa            859:         DB   XP43-$-1
0420 c9            860: XP42:   RET
0421 c3 be 04      861: XP43:   JMP  QWHAT                      ;ELSE SAY: "WHAT?"
                   862: ;
0424 cd 12 04      863: RND:    CALL PARN                       ;*** RND(EXPR) ***
0427 7c            864:         MOV  A,H                        ;EXPR MUST BE +
0428 b7            865:         ORA  A
0429 fa 9f 00      866:         JM   QHOW
042c b5            867:         ORA  L                          ;AND NON-ZERO
042d ca 9f 00      868:         JZ   QHOW
0430 d5            869:         PUSH D                          ;SAVE BOTH
0431 e5            870:         PUSH H
0432 2a 13 20      871:         LHLD RANPNT                     ;GET MEMORY AS RANDOM
0435 11 55 07      872:         LXI  D,LSTROM                   ;NUMBER
0438 e7            873:         RST  4
0439 da 38 04      874:         JC   RA1                        ;WRAP AROUND IF LAST
043c 21 00 00      875:         LXI  H,START
043f 5e            876: RA1:    MOV  E,M
0440 23            877:         INX  H
0441 56            878:         MOV  D,M
0442 22 13 20      879:         SHLD RANPNT
0445 e1            880:         POP  H
0446 eb            881:         XCHG
0447 c5            882:         PUSH B
0448 cd 5e 04      883:         CALL DIVIDE                     ;RND(N)=MOD(M,N)+1
044b c1            884:         POP  B
044c d1            885:         POP  D
044d 23            886:         INX  H
044e c9            887:         RET
                   888: ;
044f cd 12 04      889: ABS:    CALL PARN                       ;*** ABS(EXPR) ***
0452 1b            890:         DCX  D
0453 cd 7b 04      891:         CALL CHKSGN                     ;CHECK SIGN
0456 13            892:         INX  D
0457 c9            893:         RET
                   894: ;
0458 2a 15 20      895: SIZE:   LHLD TXTUNF                     ;*** SIZE ***
045b d5            896:         PUSH D                          ;GET THE NUMBER OF FREE
045c eb            897:         XCHG                            ;BYTES BETWEEN 'TXTUNF'
045d 21 87 3f      898:         LXI  H,VARBGN                   ;AND 'VARBGN'
0460 cd 74 04      899:         CALL SUBDE
0463 d1            900:         POP  D
0464 c9            901:         RET
                   902: ;
                   903: ;*************************************************************
                   904: ;
                   905: ; *** DIVIDE *** SUBDE *** CHKSGN *** CHGSGN *** & CKHLDE ***
                   906: ;
                   907: ; 'DIVIDE' DIVIDES HL BY DE, RESULT IN BC, REMAINDER IN HL
                   908: ;
                   909: ; 'SUBDE' SUBSTRACTS DE FROM HL
                   910: ;
                   911: ; 'CHKSGN' CHECKS SIGN OF HL.  IF +, NO CHANGE.  IF -, CHANGE
                   912: ; SIGN AND FLIP SIGN OF B.
                   913: ;
                   914: ; 'CHGSGN' CHECKS SIGN N OF HL AND B UNCONDITIONALLY.
                   915: ;
                   916: ; 'CKHLDE' CHECKS SIGN OF HL AND DE.  IF DIFFERENT, HL AND DE
                   917: ; ARE INTERCHANGED.  IF SAME SIGN, NOT INTERCHANGED.  EITHER
                   918: ; CASE, HL DE ARE THEN COMPARED TO SET THE FLAGS.
                   919: ;
0465 e5            920: DIVIDE: PUSH H                          ;*** DIVIDE ***
0466 6c            921:         MOV  L,H                        ;DIVIDE H BY DE
0467 26 00         922:         MVI  H,0
0469 cd 69 04      923:         CALL DV1
046c 41            924:         MOV  B,C                        ;SAVE RESULT IN B
046d 7d            925:         MOV  A,L                        ;(REMINDER+L)/DE
046e e1            926:         POP  H
046f 67            927:         MOV  H,A
0470 0e ff         928: DV1:    MVI  C,0FFH                     ;RESULT IN C
0472 0c            929: DV2:    INR  C                          ;DUMB ROUTINE
0473 cd 74 04      930:         CALL SUBDE                      ;DIVIDE BY SUBTRACT
0476 d2 6b 04      931:         JNC  DV2                        ;AND COUNT
0479 19            932:         DAD  D
047a c9            933:         RET
                   934: ;
047b 7d            935: SUBDE:  MOV  A,L                        ;*** SUBDE ***
047c 93            936:         SUB  E                          ;SUBSTRACT DE FROM
047d 6f            937:         MOV  L,A                        ;HL
047e 7c            938:         MOV  A,H
047f 9a            939:         SBB  D
0480 67            940:         MOV  H,A
0481 c9            941:         RET
                   942: ;
0482 7c            943: CHKSGN: MOV  A,H                        ;*** CHKSGN ***
0483 b7            944:         ORA  A                          ;CHECK SIGN OF HL
0484 f0            945:         RP                              ;IF -, CHANGE SIGN
                   946: ;
0485 7c            947: CHGSGN: MOV  A,H                        ;*** CHGSGN ***
0486 f5            948:         PUSH PSW
0487 2f            949:         CMA                             ;CHANGE SIGN OF HL
0488 67            950:         MOV  H,A
0489 7d            951:         MOV  A,L
048a 2f            952:         CMA
048b 6f            953:         MOV  L,A
048c 23            954:         INX  H
048d f1            955:         POP  PSW
048e ac            956:         XRA  H
048f f2 9f 00      957:         JP   QHOW
0492 78            958:         MOV  A,B                        ;AND ALSO FLIP B
0493 ee 80         959:         XRI  80H
0495 47            960:         MOV  B,A
0496 c9            961:         RET
                   962: ;
0497 7c            963: CKHLDE: MOV  A,H
0498 aa            964:         XRA  D                          ;SAME SIGN?
0499 f2 96 04      965:         JP   CK1                        ;YES, COMPARE
049c eb            966:         XCHG                            ;NO, XCH AND COMP
049d e7            967: CK1:    RST  4
049e c9            968:         RET
                   969: ;
                   970: ;*************************************************************
                   971: ;
                   972: ; *** SETVAL *** FIN *** ENDCHK *** & ERROR (& FRIENDS) ***
                   973: ;
                   974: ; "SETVAL" EXPECTS A VARIABLE, FOLLOWED BY AN EQUAL SIGN AND
                   975: ; THEN AN EXPR.  IT EVALUATES THE EXPR. AND SET THE VARIABLE
                   976: ; TO THAT VALUE.
                   977: ;
                   978: ; "FIN" CHECKS THE END OF A COMMAND.  IF IT ENDED WITH ";",
                   979: ; EXECUTION CONTINUES.  IF IT ENDED WITH A CR, IT FINDS THE
                   980: ; NEXT LINE AND CONTINUE FROM THERE.
                   981: ;
                   982: ; "ENDCHK" CHECKS IF A COMMAND IS ENDED WITH CR.  THIS IS
                   983: ; REQUIRED IN CERTAIN COMMANDS.  (GOTO, RETURN, AND STOP ETC.)
                   984: ;
                   985: ; "ERROR" PRINTS THE STRING POINTED BY DE (AND ENDS WITH CR).
                   986: ; IT THEN PRINTS THE LINE POINTED BY 'CURRNT' WITH A "?"
                   987: ; INSERTED AT WHERE THE OLD TEXT POINTER (SHOULD BE ON TOP
                   988: ; OF THE STACK) POINTS TO.  EXECUTION OF TB IS STOPPED
                   989: ; AND TBI IS RESTARTED.  HOWEVER, IF 'CURRNT' -> ZERO
                   990: ; (INDICATING A DIRECT COMMAND), THE DIRECT COMMAND IS NOT
                   991: ; PRINTED.  AND IF 'CURRNT' -> NEGATIVE # (INDICATING 'INPUT'
                   992: ; COMMAND), THE INPUT LINE IS NOT PRINTED AND EXECUTION IS
                   993: ; NOT TERMINATED BUT CONTINUED AT 'INPERR'.
                   994: ;
                   995: ; RELATED TO 'ERROR' ARE THE FOLLOWING:
                   996: ; 'QWHAT' SAVES TEXT POINTER IN STACK AND GET MESSAGE "WHAT?"
                   997: ; 'AWHAT' JUST GET MESSAGE "WHAT?" AND JUMP TO 'ERROR'.
                   998: ; 'QSORRY' AND 'ASORRY' DO SAME KIND OF THING.
                   999: ; 'AHOW' AND 'AHOW' IN THE ZERO PAGE SECTION ALSO DO THIS.
                  1000: ;
049f ff           1001: SETVAL: RST  7                          ;*** SETVAL ***
04a0 da be 04     1002:         JC   QWHAT                      ;"WHAT?" NO VARIABLE
04a3 e5           1003:         PUSH H                          ;SAVE ADDRESS OF VAR.
04a4 cf           1004:         RST  1                          ;PASS "=" SIGN
04a5 3d           1005:         DB   '='
04a6 01           1006:         DB   SV1-$-1
04a7 df           1007:         RST  3                          ;EVALUATE EXPR.
04a8 44           1008:         MOV  B,H                        ;VALUE IS IN BC NOW
04a9 4d           1009:         MOV  C,L
04aa e1           1010:         POP  H                          ;GET ADDRESS
04ab 71           1011:         MOV  M,C                        ;SAVE VALUE
04ac 23           1012:         INX  H
04ad 70           1013:         MOV  M,B
04ae c9           1014:         RET
04af c3 be 04     1015: SV1:    JMP  QWHAT                      ;NO "=" SIGN
                  1016: ;
04b2 cf           1017: FIN:    RST  1                          ;*** FIN ***
04b3 3b           1018:         DB   3BH
04b4 fd           1019:         DB   FI1-$-1
04b5 f1           1020:         POP  PSW                        ;";", PURGE RET. ADDR.
04b6 c3 57 01     1021:         JMP  RUNSML                     ;CONTINUE SAME LINE
04b9 cf           1022: FI1:    RST  1                          ;NOT ";", IS IT CR?
04ba 0d           1023:         DB   CR
04bb fd           1024:         DB   FI2-$-1
04bc f1           1025:         POP  PSW                        ;YES, PURGE RET. ADDR.
04bd c3 47 01     1026:         JMP  RUNNXL                     ;RUN NEXT LINE
04c0 c9           1027: FI2:    RET                             ;ELSE RETURN TO CALLER
                  1028: ;
04c1 ef           1029: ENDCHK: RST  5                          ;*** ENDCHK ***
04c2 fe 0d        1030:         CPI  CR                         ;END WITH CR?
04c4 c8           1031:         RZ                              ;OK, ELSE SAY: "WHAT?"
                  1032: ;
04c5 d5           1033: QWHAT:  PUSH D                          ;*** QWHAT ***
04c6 11 ae 00     1034: AWHAT:  LXI  D,WHAT                     ;*** AWHAT ***
04c9 97           1035: ERROR:  SUB  A                          ;*** ERROR ***
04ca cd 58 05     1036:         CALL PRTSTG                     ;PRINT 'WHAT?', 'HOW?'
04cd d1           1037:         POP  D                          ;OR 'SORRY'
04ce 1a           1038:         LDAX D                          ;SAVE THE CHARACTER
04cf f5           1039:         PUSH PSW                        ;AT WHERE OLD DE ->
04d0 97           1040:         SUB  A                          ;AND PUT A 0 THERE
04d1 12           1041:         STAX D
04d2 2a 01 20     1042:         LHLD CURRNT                     ;GET CURRENT LINE #
04d5 e5           1043:         PUSH H
04d6 7e           1044:         MOV  A,M                        ;CHECK THE VALUE
04d7 23           1045:         INX  H
04d8 b6           1046:         ORA  M
04d9 d1           1047:         POP  D
04da ca ba 00     1048:         JZ   RSTART                     ;IF ZERO, JUST RESTART
04dd 7e           1049:         MOV  A,M                        ;IF NEGATIVE,
04de b7           1050:         ORA  A
04df fa bf 02     1051:         JM   INPERR                     ;REDO INPUT
04e2 cd ca 05     1052:         CALL PRTLN                      ;ELSE PRINT THE LINE
04e5 1b           1053:         DCX  D                          ;UPTO WHERE THE 0 IS
04e6 f1           1054:         POP  PSW                        ;RESTORE THE CHARACTER
04e7 12           1055:         STAX D
04e8 3e 3f        1056:         MVI  A,3FH                      ;PRINT A "?"
04ea d7           1057:         RST  2
04eb 97           1058:         SUB  A                          ;AND THE REST OF THE
04ec cd 58 05     1059:         CALL PRTSTG                     ;LINE
04ef c3 ba 00     1060:         JMP  RSTART                     ;THEN RESTART
                  1061: ;
04f2 d5           1062: QSORRY: PUSH D                          ;*** QSORRY ***
04f3 11 b4 00     1063: ASORRY: LXI  D,SORRY                    ;*** ASORRY ***
04f6 c3 c2 04     1064:         JMP  ERROR
                  1065: ;
                  1066: ;*************************************************************
                  1067: ;
                  1068: ; *** GETLN *** FNDLN (& FRIENDS) ***
                  1069: ;
                  1070: ; 'GETLN' READS A INPUT LINE INTO 'BUFFER'.  IT FIRST PROMPT
                  1071: ; THE CHARACTER IN A (GIVEN BY THE CALLER), THEN IT FILLS
                  1072: ; THE BUFFER AND ECHOS.  IT IGNORES LF'S AND NULLS, BUT STILL
                  1073: ; ECHOS THEM BACK.  RUB-OUT IS USED TO CAUSE IT TO DELETE
                  1074: ; THE LAST CHARACTER (IF THERE IS ONE), AND ALT-MOD IS USED TO
                  1075: ; CAUSE IT TO DELETE THE WHOLE LINE AND START IT ALL OVER.
                  1076: ; CR SIGNALS THE END OF A LINE, AND CAUSE 'GETLN' TO RETURN.
                  1077: ;
                  1078: ; 'FNDLN' FINDS A LINE WITH A GIVEN LINE # (IN HL) IN THE
                  1079: ; TEXT SAVE AREA.  DE IS USED AS THE TEXT POINTER.  IF THE
                  1080: ; LINE IS FOUND, DE WILL POINT TO THE BEGINNING OF THAT LINE
                  1081: ; (I.E., THE LOW BYTE OF THE LINE #), AND FLAGS ARE NC & Z.
                  1082: ; IF THAT LINE IS NOT THERE AND A LINE WITH A HIGHER LINE #
                  1083: ; IS FOUND, DE POINTS TO THERE AND FLAGS ARE NC & NZ.  IF
                  1084: ; WE REACHED THE END OF TEXT SAVE AREA AND CANNOT FIND THE
                  1085: ; LINE, FLAGS ARE C & NZ.
                  1086: ; 'FNDLN' WILL INITIALIZE DE TO THE BEGINNING OF THE TEXT SAVE
                  1087: ; AREA TO START THE SEARCH.  SOME OTHER ENTRIES OF THIS
                  1088: ; ROUTINE WILL NOT INITIALIZE DE AND DO THE SEARCH.
                  1089: ; 'FNDLNP' WILL START WITH DE AND SEARCH FOR THE LINE #.
                  1090: ; 'FNDNXT' WILL BUMP DE BY 2, FIND A CR AND THEN START SEARCH.
                  1091: ; 'FNDSKP' USE DE TO FIND A CR, AND THEN START SEARCH.
                  1092: ;
04f9 d7           1093: GETLN:  RST  2                          ;*** GETLN ***
04fa 11 be 3f     1094:         LXI  D,BUFFER                   ;PROMPT AND INIT.
04fd cd 6e 06     1095: GL1:    CALL CHKIO                      ;CHECK KEYBOARD
0500 ca f6 04     1096:         JZ   GL1                        ;NO INPUT, WAIT
0503 fe 7f        1097:         CPI  7FH                        ;DELETE LAST CHARACTER?
0505 ca 1b 05     1098:         JZ   GL3                        ;YES
0508 d7           1099:         RST  2                          ;INPUT, ECHO BACK
0509 fe 0a        1100:         CPI  0AH                        ;IGNORE LF
050b ca f6 04     1101:         JZ   GL1
050e b7           1102:         ORA  A                          ;IGNORE NULL
050f ca f6 04     1103:         JZ   GL1
0512 fe 7d        1104:         CPI  7DH                        ;DELETE THE WHOLE LINE?
0514 ca 28 05     1105:         JZ   GL4                        ;YES
0517 12           1106:         STAX D                          ;ELSE SAVE INPUT
0518 13           1107:         INX  D                          ;AND BUMP POINTER
0519 fe 0d        1108:         CPI  0DH                        ;WAS IT CR?
051b c8           1109:         RZ                              ;YES, END OF LINE
051c 7b           1110:         MOV  A,E                        ;ELSE MORE FREE ROOM?
051d fe fe        1111:         CPI  BUFEND & 0FFH
051f c2 f6 04     1112:         JNZ  GL1                        ;YES, GET NEXT INPUT
0522 7b           1113: GL3:    MOV  A,E                        ;DELETE LAST CHARACTER
0523 fe be        1114:         CPI  BUFFER & 0FFH            ;BUT DO WE HAVE ANY?
0525 ca 28 05     1115:         JZ   GL4                        ;NO, REDO WHOLE LINE
0528 1b           1116:         DCX  D                          ;YES, BACKUP POINTER
0529 3e 5c        1117:         MVI  A,5CH                      ;AND ECHO A BACK-SLASH
052b d7           1118:         RST  2
052c c3 f6 04     1119:         JMP  GL1                        ;GO GET NEXT INPUT
052f cd 0e 00     1120: GL4:    CALL CRLF                       ;REDO ENTIRE LINE
0532 3e 5e        1121:         MVI  A,05EH                     ;CR, LF AND UP-ARROW
0534 c3 f2 04     1122:         JMP  GETLN
                  1123: ;
0537 7c           1124: FNDLN:  MOV  A,H                        ;*** FNDLN ***
0538 b7           1125:         ORA  A                          ;CHECK SIGN OF HL
0539 fa 9f 00     1126:         JM   QHOW                       ;IT CANNOT BE -
053c 11 17 20     1127:         LXI  D,TXTBGN                   ;INIT TEXT POINTER
                  1128: ;
                  1129: FNDLP:                                  ;*** FDLNP ***
053f e5           1130: FL1:    PUSH H                          ;SAVE LINE #
0540 2a 15 20     1131:         LHLD TXTUNF                     ;CHECK IF WE PASSED END
0543 2b           1132:         DCX  H
0544 e7           1133:         RST  4
0545 e1           1134:         POP  H                          ;GET LINE # BACK
0546 d8           1135:         RC                              ;C,NZ PASSED END
0547 1a           1136:         LDAX D                          ;WE DID NOT, GET BYTE 1
0548 95           1137:         SUB  L                          ;IS THIS THE LINE?
0549 47           1138:         MOV  B,A                        ;COMPARE LOW ORDER
054a 13           1139:         INX  D
054b 1a           1140:         LDAX D                          ;GET BYTE 2
054c 9c           1141:         SBB  H                          ;COMPARE HIGH ORDER
054d da 4d 05     1142:         JC   FL2                        ;NO, NOT THERE YET
0550 1b           1143:         DCX  D                          ;ELSE WE EITHER FOUND
0551 b0           1144:         ORA  B                          ;IT, OR IT IS NOT THERE
0552 c9           1145:         RET                             ;NC,Z:FOUND, NC,NZ:NO
                  1146: ;
                  1147: FNDNXT:                                 ;*** FNDNXT ***
0553 13           1148:         INX  D                          ;FIND NEXT LINE
0554 13           1149: FL2:    INX  D                          ;JUST PASSED BYTE 1 & 2
                  1150: ;
0555 1a           1151: FNDSKP: LDAX D                          ;*** FNDSKP ***
0556 fe 0d        1152:         CPI  CR                         ;TRY TO FIND CR
0558 c2 4d 05     1153:         JNZ  FL2                        ;KEEP LOOKING
055b 13           1154:         INX  D                          ;FOUND CR, SKIP OVER
055c c3 38 05     1155:         JMP  FL1                        ;CHECK IF END OF TEXT
                  1156: ;
                  1157: ;*************************************************************
                  1158: ;
                  1159: ; *** PRTSTG *** QTSTG *** PRTNUM *** & PRTLN ***
                  1160: ;
                  1161: ; 'PRTSTG' PRINTS A STRING POINTED BY DE.  IT STOPS PRINTING
                  1162: ; AND RETURNS TO CALLER WHEN EITHER A CR IS PRINTED OR WHEN
                  1163: ; THE NEXT BYTE IS THE SAME AS WHAT WAS IN A (GIVEN BY THE
                  1164: ; CALLER).  OLD A IS STORED IN B, OLD B IS LOST.
                  1165: ;
                  1166: ; 'QTSTG' LOOKS FOR A BACK-ARROW, SINGLE QUOTE, OR DOUBLE
                  1167: ; QUOTE.  IF NONE OF THESE, RETURN TO CALLER.  IF BACK-ARROW,
                  1168: ; OUTPUT A CR WITHOUT A LF.  IF SINGLE OR DOUBLE QUOTE, PRINT
                  1169: ; THE STRING IN THE QUOTE AND DEMANDS A MATCHING UNQUOTE.
                  1170: ; AFTER THE PRINTING THE NEXT 3 BYTES OF THE CALLER IS SKIPPED
                  1171: ; OVER (USUALLY A JUMP INSTRUCTION.
                  1172: ;
                  1173: ; 'PRTNUM' PRINTS THE NUMBER IN HL.  LEADING BLANKS ARE ADDED
                  1174: ; IF NEEDED TO PAD THE NUMBER OF SPACES TO THE NUMBER IN C.
                  1175: ; HOWEVER, IF THE NUMBER OF DIGITS IS LARGER THAN THE # IN
                  1176: ; C, ALL DIGITS ARE PRINTED ANYWAY.  NEGATIVE SIGN IS ALSO
                  1177: ; PRINTED AND COUNTED IN, POSITIVE SIGN IS NOT.
                  1178: ;
                  1179: ; 'PRTLN' PRINTS A SAVED TEXT LINE WITH LINE # AND ALL.
                  1180: ;
055f 47           1181: PRTSTG: MOV  B,A                        ;*** PRTSTG ***
0560 1a           1182: PS1:    LDAX D                          ;GET A CHARACTER
0561 13           1183:         INX  D                          ;BUMP POINTER
0562 b8           1184:         CMP  B                          ;SAME AS OLD A?
0563 c8           1185:         RZ                              ;YES, RETURN
0564 d7           1186:         RST  2                          ;ELSE PRINT IT
0565 fe 0d        1187:         CPI  CR                         ;WAS IT A CR?
0567 c2 59 05     1188:         JNZ  PS1                        ;NO, NEXT
056a c9           1189:         RET                             ;YES, RETURN
                  1190: ;
056b cf           1191: QTSTG:  RST  1                          ;*** QTSTG ***
056c 22           1192:         DB   '"'
056d 08           1193:         DB   QT3-$-1
056e 3e 22        1194:         MVI  A,22H                      ;IT IS A "
0570 cd 58 05     1195: QT1:    CALL PRTSTG                     ;PRINT UNTIL ANOTHER
0573 fe 0d        1196:         CPI  CR                         ;WAS LAST ONE A CR?
0575 e1           1197:         POP  H                          ;RETURN ADDRESS
0576 ca 47 01     1198:         JZ   RUNNXL                     ;WAS CR, RUN NEXT LINE
0579 23           1199: QT2:    INX  H                          ;SKIP 3 BYTES ON RETURN
057a 23           1200:         INX  H
057b 23           1201:         INX  H
057c e9           1202:         PCHL                            ;RETURN
057d cf           1203: QT3:    RST  1                          ;IS IT A '?
057e 27           1204:         DB   27H
057f fe           1205:         DB   QT4-$-1
0580 3e 27        1206:         MVI  A,27H                      ;YES, DO THE SAME
0582 c3 69 05     1207:         JMP  QT1                        ;AS IN "
0585 cf           1208: QT4:    RST  1                          ;IS IT BACK-ARROW?
0586 5f           1209:         DB   5FH
0587 01           1210:         DB   QT5-$-1
0588 3e 8d        1211:         MVI  A,08DH                     ;YES, CR WITHOUT LF
058a d7           1212:         RST  2                          ;DO IT TWICE TO GIVE
058b d7           1213:         RST  2                          ;TTY ENOUGH TIME
058c e1           1214:         POP  H                          ;RETURN ADDRESS
058d c3 72 05     1215:         JMP  QT2
0590 c9           1216: QT5:    RET                             ;NONE OF ABOVE
                  1217: ;
0591 06 00        1218: PRTNUM: MVI  B,0                        ;*** PRTNUM ***
0593 cd 7b 04     1219:         CALL CHKSGN                     ;CHECK SIGN
0596 f2 95 05     1220:         JP   PN1                        ;NO SIGN
0599 06 2d        1221:         MVI  B,'-'                      ;B=SIGN
059b 0d           1222:         DCR  C                          ;'-' TAKES SPACE
059c d5           1223: PN1:    PUSH D                          ;SAVE
059d 11 0a 00     1224:         LXI  D,0AH                      ;DECIMAL
05a0 d5           1225:         PUSH D                          ;SAVE AS A FLAG
05a1 0d           1226:         DCR  C                          ;C=SPACES
05a2 c5           1227:         PUSH B                          ;SAVE SIGN & SPACE
05a3 cd 5e 04     1228: PN2:    CALL DIVIDE                     ;DIVIDE HL BY 10
05a6 78           1229:         MOV  A,B                        ;RESULT 0?
05a7 b1           1230:         ORA  C
05a8 ca ac 05     1231:         JZ   PN3                        ;YES, WE GOT ALL
05ab e3           1232:         XTHL                            ;NO, SAVE REMAINDER
05ac 2d           1233:         DCR  L                          ;AND COUNT SPACE
05ad e5           1234:         PUSH H                          ;HL IS OLD BC
05ae 60           1235:         MOV  H,B                        ;MOVE RESULT TO BC
05af 69           1236:         MOV  L,C
05b0 c3 9c 05     1237:         JMP  PN2                        ;AND DIVIDE BY 10
05b3 c1           1238: PN3:    POP  B                          ;WE GOT ALL DIGITS IN
05b4 0d           1239: PN4:    DCR  C                          ;THE STACK
05b5 79           1240:         MOV  A,C                        ;LOOK AT SPACE COUNT
05b6 b7           1241:         ORA  A
05b7 fa b9 05     1242:         JM   PN5                        ;NO LEADING BLANKS
05ba 3e 20        1243:         MVI  A,20H                      ;LEADING BLANKS
05bc d7           1244:         RST  2
05bd c3 ad 05     1245:         JMP  PN4                        ;MORE?
05c0 78           1246: PN5:    MOV  A,B                        ;PRINT SIGN
05c1 b7           1247:         ORA  A
05c2 c4 10 00     1248:         CNZ  10H
05c5 5d           1249:         MOV  E,L                        ;LAST REMAINDER IN E
05c6 7b           1250: PN6:    MOV  A,E                        ;CHECK DIGIT IN E
05c7 fe 0a        1251:         CPI  0AH                        ;10 IS FLAG FOR NO MORE
05c9 d1           1252:         POP  D
05ca c8           1253:         RZ                              ;IF SO, RETURN
05cb c6 30        1254:         ADI  30H                        ;ELSE CONVERT TO ASCII
05cd d7           1255:         RST  2                          ;AND PRINT THE DIGIT
05ce c3 bf 05     1256:         JMP  PN6                        ;GO BACK FOR MORE
                  1257: ;
05d1 1a           1258: PRTLN:  LDAX D                          ;*** PRTLN ***
05d2 6f           1259:         MOV  L,A                        ;LOW ORDER LINE #
05d3 13           1260:         INX  D
05d4 1a           1261:         LDAX D                          ;HIGH ORDER
05d5 67           1262:         MOV  H,A
05d6 13           1263:         INX  D
05d7 0e 04        1264:         MVI  C,4H                       ;PRINT 4 DIGIT LINE #
05d9 cd 8a 05     1265:         CALL PRTNUM
05dc 3e 20        1266:         MVI  A,20H                      ;FOLLOWED BY A BLANK
05de d7           1267:         RST  2
05df 97           1268:         SUB  A                          ;AND THEN THE NEXT
05e0 cd 58 05     1269:         CALL PRTSTG
05e3 c9           1270:         RET
                  1271: ;
                  1272: ;*************************************************************
                  1273: ;
                  1274: ; *** MVUP *** MVDOWN *** POPA *** & PUSHA ***
                  1275: ;
                  1276: ; 'MVUP' MOVES A BLOCK UP FROM WHERE DE-> TO WHERE BC-> UNTIL
                  1277: ; DE = HL
                  1278: ;
                  1279: ; 'MVDOWN' MOVES A BLOCK DOWN FROM WHERE DE-> TO WHERE HL->
                  1280: ; UNTIL DE = BC
                  1281: ;
                  1282: ; 'POPA' RESTORES THE 'FOR' LOOP VARIABLE SAVE AREA FROM THE
                  1283: ; STACK
                  1284: ;
                  1285: ; 'PUSHA' STACKS THE 'FOR' LOOP VARIABLE SAVE AREA INTO THE
                  1286: ; STACK
                  1287: ;
05e4 e7           1288: MVUP:   RST  4                          ;*** MVUP ***
05e5 c8           1289:         RZ                              ;DE = HL, RETURN
05e6 1a           1290:         LDAX D                          ;GET ONE BYTE
05e7 02           1291:         STAX B                          ;MOVE IT
05e8 13           1292:         INX  D                          ;INCREASE BOTH POINTERS
05e9 03           1293:         INX  B
05ea c3 dd 05     1294:         JMP  MVUP                       ;UNTIL DONE
                  1295: ;
05ed 78           1296: MVDOWN: MOV  A,B                        ;*** MVDOWN ***
05ee 92           1297:         SUB  D                          ;TEST IF DE = BC
05ef c2 ee 05     1298:         JNZ  MD1                        ;NO, GO MOVE
05f2 79           1299:         MOV  A,C                        ;MAYBE, OTHER BYTE?
05f3 93           1300:         SUB  E
05f4 c8           1301:         RZ                              ;YES, RETURN
05f5 1b           1302: MD1:    DCX  D                          ;ELSE MOVE A BYTE
05f6 2b           1303:         DCX  H                          ;BUT FIRST DECREASE
05f7 1a           1304:         LDAX D                          ;BOTH POINTERS AND
05f8 77           1305:         MOV  M,A                        ;THEN DO IT
05f9 c3 e6 05     1306:         JMP  MVDOWN                     ;LOOP BACK
                  1307: ;
05fc c1           1308: POPA:   POP  B                          ;BC = RETURN ADDR.
05fd e1           1309:         POP  H                          ;RESTORE LOPVAR, BUT
05fe 22 09 20     1310:         SHLD LOPVAR                     ;=0 MEANS NO MORE
0601 7c           1311:         MOV  A,H
0602 b5           1312:         ORA  L
0603 ca 0f 06     1313:         JZ   PP1                        ;YEP, GO RETURN
0606 e1           1314:         POP  H                          ;NOP, RESTORE OTHERS
0607 22 0b 20     1315:         SHLD LOPINC
060a e1           1316:         POP  H
060b 22 0d 20     1317:         SHLD LOPLMT
060e e1           1318:         POP  H
060f 22 0f 20     1319:         SHLD LOPLN
0612 e1           1320:         POP  H
0613 22 11 20     1321:         SHLD LOPPT
0616 c5           1322: PP1:    PUSH B                          ;BC = RETURN ADDR.
0617 c9           1323:         RET
                  1324: ;
0618 21 00 b8     1325: PUSHA:  LXI  H,STKLMT                   ;*** PUSHA ***
061b cd 7e 04     1326:         CALL CHGSGN
061e c1           1327:         POP  B                          ;BC=RETURN ADDRESS
061f 39           1328:         DAD  SP                         ;IS STACK NEAR THE TOP?
0620 d2 eb 04     1329:         JNC  QSORRY                     ;YES, SORRY FOR THAT
0623 2a 09 20     1330:         LHLD LOPVAR                     ;ELSE SAVE LOOP VAR'S
0626 7c           1331:         MOV  A,H                        ;BUT IF LOPVAR IS 0
0627 b5           1332:         ORA  L                          ;THAT WILL BE ALL
0628 ca 37 06     1333:         JZ   PU1
062b 2a 11 20     1334:         LHLD LOPPT                      ;ELSE, MORE TO SAVE
062e e5           1335:         PUSH H
062f 2a 0f 20     1336:         LHLD LOPLN
0632 e5           1337:         PUSH H
0633 2a 0d 20     1338:         LHLD LOPLMT
0636 e5           1339:         PUSH H
0637 2a 0b 20     1340:         LHLD LOPINC
063a e5           1341:         PUSH H
063b 2a 09 20     1342:         LHLD LOPVAR
063e e5           1343: PU1:    PUSH H
063f c5           1344:         PUSH B                          ;BC = RETURN ADDR.
0640 c9           1345:         RET
                  1346: ;
                  1347: ;*************************************************************
                  1348: ;
                  1349: ; *** OUTC *** & CHKIO ***
                  1350: ;
                  1351: ; THESE ARE THE ONLY I/O ROUTINES IN TBI.
                  1352: ; 'OUTC' IS CONTROLLED BY A SOFTWARE SWITCH 'OCSW'.  IF OCSW=0
                  1353: ; 'OUTC' WILL JUST RETURN TO THE CALLER.  IF OCSW IS NOT 0,
                  1354: ; IT WILL OUTPUT THE BYTE IN A.  IF THAT IS A CR, A LF IS ALSO
                  1355: ; SEND OUT.  ONLY THE FLAGS MAY BE CHANGED AT RETURN. ALL REG.
                  1356: ; ARE RESTORED.
                  1357: ;
                  1358: ; 'CHKIO' CHECKS THE INPUT.  IF NO INPUT, IT WILL RETURN TO
                  1359: ; THE CALLER WITH THE Z FLAG SET.  IF THERE IS INPUT, Z FLAG
                  1360: ; IS CLEARED AND THE INPUT BYTE IS IN A.  HOWEVER, IF THE
                  1361: ; INPUT IS A CONTROL-O, THE 'OCSW' SWITCH IS COMPLIMENTED, AND
                  1362: ; Z FLAG IS RETURNED.  IF A CONTROL-C IS READ, 'CHKIO' WILL
                  1363: ; RESTART TBI AND DO NOT RETURN TO THE CALLER.
                  1364: ;
                  1365: ;OUTC:  PUSH PSW                        ;THIS IS AT LOC. 10
                  1366: ;       LDA  OCSW                       ;CHECK SOFTWARE SWITCH
                  1367: ;       ORA  A
                  1368: 
(f828)            1369: CONIN_ADDR		EQU 0F828h
(f830)            1370: CONOUT_ADDR		EQU 0F830h
(f820)            1371: CONSTAT_ADDR 	EQU 0F820h
                  1372: 
0641 32 00 20     1373: INIT:   STA  OCSW
                  1374:         ;MVI  A,3                        ;RESET ACIA
                  1375:         ;OUT  16
                  1376:         ;MVI  A,15H			;15H FOR 8N1, 11H FOR 8N2
                  1377:         ;OUT  16
0644 16 19        1378:         MVI  D,19H
                  1379: PATLOP:
0646 cd 0e 00     1380:         CALL CRLF
0649 15           1381:         DCR  D
064a c2 3f 06     1382:         JNZ  PATLOP
064d 97           1383:         SUB  A
064e 11 8f 06     1384:         LXI  D,MSG1
0651 cd 58 05     1385:         CALL PRTSTG
0654 21 00 00     1386:         LXI  H,START
0657 22 13 20     1387:         SHLD RANPNT
065a 21 17 20     1388:         LXI  H,TXTBGN
065d 22 15 20     1389:         SHLD TXTUNF
0660 c3 ba 00     1390:         JMP  RSTART
0663 c2 61 06     1391: OC2:    JNZ  OC3                        ;IT IS ON
0666 f1           1392:         POP  PSW                        ;IT IS OFF
0667 c9           1393:         RET                             ;RESTORE AF AND RETURN
                  1394: 
0668 f1           1395: OC3:	POP PSW
0669 cd 30 f8     1396: 		CALL CONOUT_ADDR
                  1397: 		;IN   16                         ;COME HERE TO DO OUTPUT
                  1398:         ;ANI  2H                         ;STATUS BIT
                  1399:         ;JZ   OC3                        ;NOT READY, WAIT
                  1400:         ;POP  PSW                        ;READY, GET OLD A BACK
                  1401:         ;OUT  17                         ;AND SEND IT OUT
066c fe 0d        1402:         CPI  CR                         ;WAS IT CR?
066e c0           1403:         RNZ                             ;NO, FINISHED
066f 3e 0a        1404:         MVI  A,LF                       ;YES, WE SEND LF TOO
0671 d7           1405:         RST  2                          ;THIS IS RECURSIVE
0672 3e 0d        1406:         MVI  A,CR                       ;GET CR BACK IN A
0674 c9           1407:         RET
                  1408: ;
0675 cd 20 f8     1409: CHKIO:  CALL CONSTAT_ADDR
0678 c2 75 06     1410: 		JNZ CHKIO_CNT
067b c8           1411: 		RZ
                  1412: 		;IN   16                         ;*** CHKIO ***
                  1413:         ;NOP                             ;STATUS BIT FLIPPED?
                  1414:         ;ANI  1H                         ;MASK STATUS BIT
                  1415:         ;RZ                              ;NOT READY, RETURN "Z"
                  1416: CHKIO_CNT:
                  1417:         ;IN   17                         ;READY, READ DATA
067c cd 28 f8     1418:         CALL CONIN_ADDR
067f e6 7f        1419: 		ANI  7FH                        ;MASK BIT 7 OFF
0681 fe 0f        1420: 		CPI  0FH                        ;IS IT CONTROL-O?
0683 c2 89 06     1421:         JNZ  CI1                        ;NO, MORE CHECKING
0686 3a 00 20     1422:         LDA  OCSW                       ;CONTROL-O FLIPS OCSW
0689 2f           1423:         CMA                             ;ON TO OFF, OFF TO ON
068a 32 00 20     1424:         STA  OCSW
068d c3 6e 06     1425:         JMP  CHKIO                      ;GET ANOTHER INPUT
0690 fe 03        1426: CI1:    CPI  3H                         ;IS IT CONTROL-C?
0692 c0           1427:         RNZ                             ;NO, RETURN "NZ"
0693 c3 ba 00     1428:         JMP  RSTART                     ;YES, RESTART TBI
                  1429: ;
0696 54 49 4e 59  1430: MSG1:   DB   'TINY '
     20 
069b 42 41 53 49  1431:         DB   'BASIC'
     43 
06a0 0d           1432:         DB   CR
                  1433: ;
                  1434: ;*************************************************************
                  1435: ;
                  1436: ; *** TABLES *** DIRECT *** & EXEC ***
                  1437: ;
                  1438: ; THIS SECTION OF THE CODE TESTS A STRING AGAINST A TABLE.
                  1439: ; WHEN A MATCH IS FOUND, CONTROL IS TRANSFERED TO THE SECTION
                  1440: ; OF CODE ACCORDING TO THE TABLE.
                  1441: ;
                  1442: ; AT 'EXEC', DE SHOULD POINT TO THE STRING AND HL SHOULD POINT
                  1443: ; TO THE TABLE-1.  AT 'DIRECT', DE SHOULD POINT TO THE STRING.
                  1444: ; HL WILL BE SET UP TO POINT TO TAB1-1, WHICH IS THE TABLE OF
                  1445: ; ALL DIRECT AND STATEMENT COMMANDS.
                  1446: ;
                  1447: ; A '.' IN THE STRING WILL TERMINATE THE TEST AND THE PARTIAL
                  1448: ; MATCH WILL BE CONSIDERED AS A MATCH.  E.G., 'P.', 'PR.',
                  1449: ; 'PRI.', 'PRIN.', OR 'PRINT' WILL ALL MATCH 'PRINT'.
                  1450: ;
                  1451: ; THE TABLE CONSISTS OF ANY NUMBER OF ITEMS.  EACH ITEM
                  1452: ; IS A STRING OF CHARACTERS WITH BIT 7 SET TO 0 AND
                  1453: ; A JUMP ADDRESS STORED HI-LOW WITH BIT 7 OF THE HIGH
                  1454: ; BYTE SET TO 1.
                  1455: ;
                  1456: ; END OF TABLE IS AN ITEM WITH A JUMP ADDRESS ONLY.  IF THE
                  1457: ; STRING DOES NOT MATCH ANY OF THE OTHER ITEMS, IT WILL
                  1458: ; MATCH THIS NULL ITEM AS DEFAULT.
                  1459: ;
                  1460: 
                  1461: ;	DB (loop / 16) + 128
                  1462: ;	DB loop
                  1463: 	
                  1464: TAB1:                                   ;DIRECT COMMANDS
06a1 4c 49 53 54  1465:         DB   'LIST'
                  1466:         ;DWA  LIST
06a5 96           1467: 		DB (LIST / 16) + 128
06a6 6f           1468: 		DB LIST	
06a7 52 55 4e     1469:         DB   'RUN'
                  1470:         ;DWA  RUN
06aa 94           1471: 		DB (RUN / 16) + 128
06ab 41           1472: 		DB RUN
06ac 4e 45 57     1473:         DB   'NEW'
                  1474:         ;DWA  NEW
06af 93           1475: 		DB (NEW / 16) + 128
06b0 32           1476: 		DB NEW
                  1477: ;
                  1478: TAB2:                                   ;DIRECT/STATEMENT
06b1 4e 45 58 54  1479:         DB   'NEXT'
                  1480:         ;DWA  NEXT
06b5 a5           1481: 		DB (NEXT / 16) + 128
06b6 53           1482: 		DB NEXT
06b7 4c 45 54     1483:         DB   'LET'
                  1484:         ;DWA  LET
06ba b1           1485: 		DB (LET / 16) + 128
06bb 1f           1486: 		DB LET
06bc 49 46        1487:         DB   'IF'
                  1488:         ;DWA  IFF
06be ab           1489: 		DB (IFF / 16) + 128
06bf b0           1490: 		DB IFF
06c0 47 4f 54 4f  1491:         DB   'GOTO'
                  1492:         ;DWA  GOTO
06c4 96           1493: 		DB (GOTO / 16) + 128
06c5 60           1494: 		DB GOTO
06c6 47 4f 53 55  1495:         DB   'GOSUB'
     42 
                  1496:         ;DWA  GOSUB
06cb 9b           1497: 		DB (GOSUB / 16) + 128
06cc bf           1498: 		DB GOSUB
06cd 52 45 54 55  1499:         DB   'RETURN'
     52 4e 
                  1500:         ;DWA  RETURN
06d3 9d           1501: 		DB (RETURN / 16) + 128
06d4 df           1502: 		DB RETURN
06d5 52 45 4d     1503:         DB   'REM'
                  1504:         ;DWA  REM
06d8 aa           1505: 		DB (REM / 16) + 128
06d9 ac           1506: 		DB REM
06da 46 4f 52     1507:         DB   'FOR'
                  1508:         ;DWA  FOR
06dd 9f           1509: 		DB (FOR / 16) + 128
06de f8           1510: 		DB FOR
06df 49 4e 50 55  1511:         DB   'INPUT'
     54 
                  1512:         ;DWA  INPUT
06e4 ac           1513: 		DB (INPUT / 16) + 128
06e5 c9           1514: 		DB INPUT
06e6 50 52 49 4e  1515:         DB   'PRINT'
     54 
                  1516:         ;DWA  PRINT
06eb 98           1517: 		DB (PRINT / 16) + 128
06ec 87           1518: 		DB PRINT
06ed 53 54 4f 50  1519:         DB   'STOP'
                  1520:         ;DWA  STOP
06f1 93           1521: 		DB (STOP / 16) + 128
06f2 3b           1522: 		DB STOP
                  1523:         ;DWA  DEFLT
06f3 b1           1524: 		DB (DEFLT / 16) + 128
06f4 19           1525: 		DB DEFLT
                  1526: ;
                  1527: TAB4:                                   ;FUNCTIONS
06f5 52 4e 44     1528:         DB   'RND'
                  1529:         ;DWA  RND
06f8 c1           1530: 		DB (RND / 16) + 128
06f9 1d           1531: 		DB RND
06fa 41 42 53     1532:         DB   'ABS'
                  1533:         ;DWA  ABS
06fd c4           1534: 		DB (ABS / 16) + 128
06fe 48           1535: 		DB ABS
06ff 53 49 5a 45  1536:         DB   'SIZE'
                  1537:         ;DWA  SIZE
0703 c5           1538: 		DB (SIZE / 16) + 128
0704 51           1539: 		DB SIZE
                  1540:         ;DWA  XP40
0705 c0           1541: 		DB (XP40 / 16) + 128
0706 03           1542: 		DB XP40
                  1543: ;
                  1544: TAB5:                                   ;"TO" IN "FOR"
0707 54 4f        1545:         DB   'TO'
                  1546:         ;DWA  FR1
0709 a0           1547: 		DB (FR1 / 16) + 128
070a 06           1548: 		DB FR1
                  1549:         ;DWA  QWHAT
070b cb           1550: 		DB (QWHAT / 16) + 128
070c be           1551: 		DB QWHAT
                  1552: ;
                  1553: TAB6:                                   ;"STEP" IN "FOR"
070d 53 54 45 50  1554:         DB   'STEP'
                  1555:         ;DWA  FR2
0711 a0           1556: 		DB (FR2 / 16) + 128
0712 0e           1557: 		DB FR2
                  1558:         ;DWA  FR3
0713 a1           1559: 		DB (FR3 / 16) + 128
0714 12           1560: 		DB FR3
                  1561: ;
                  1562: TAB8:                                   ;RELATION OPERATORS
0715 3e 3d        1563:         DB   '>='
                  1564:         ;DWA  XP11
0717 b2           1565: 		DB (XP11 / 16) + 128
0718 2d           1566: 		DB XP11
0719 23           1567:         DB   '#'
                  1568:         ;DWA  XP12
071a b3           1569: 		DB (XP12 / 16) + 128
071b 33           1570: 		DB XP12
071c 3e           1571:         DB   '>'
                  1572:         ;DWA  XP13
071d b3           1573: 		DB (XP13 / 16) + 128
071e 39           1574: 		DB XP13
071f 3d           1575:         DB   '='
                  1576:         ;DWA  XP15
0720 b4           1577: 		DB (XP15 / 16) + 128
0721 48           1578: 		DB XP15
0722 3c 3d        1579:         DB   '<='
                  1580:         ;DWA  XP14
0724 b4           1581: 		DB (XP14 / 16) + 128
0725 40           1582: 		DB XP14
0726 3c           1583:         DB   '<'
                  1584:         ;DWA  XP16
0727 b4           1585: 		DB (XP16 / 16) + 128
0728 4e           1586: 		DB XP16
                  1587:         ;DWA  XP17
0729 b5           1588: 		DB (XP17 / 16) + 128
072a 54           1589: 		DB XP17
                  1590: ;
072b 21 99 06     1591: DIRECT: LXI  H,TAB1-1                   ;*** DIRECT ***
                  1592: ;
                  1593: EXEC:                                   ;*** EXEC ***
072e ef           1594: EX0:    RST  5                          ;IGNORE LEADING BLANKS
072f d5           1595:         PUSH D                          ;SAVE POINTER
0730 1a           1596: EX1:    LDAX D                          ;IF FOUND '.' IN STRING
0731 13           1597:         INX  D                          ;BEFORE ANY MISMATCH
0732 fe 2e        1598:         CPI  2EH                        ;WE DECLARE A MATCH
0734 ca 46 07     1599:         JZ   EX3
0737 23           1600:         INX  H                          ;HL->TABLE
0738 be           1601:         CMP  M                          ;IF MATCH, TEST NEXT
0739 ca 29 07     1602:         JZ   EX1
073c 3e 7f        1603:         MVI  A,07FH                     ;ELSE SEE IF BIT 7
073e 1b           1604:         DCX  D                          ;OF TABLE IS SET, WHICH
073f be           1605:         CMP  M                          ;IS THE JUMP ADDR. (HI)
0740 da 4d 07     1606:         JC   EX5                        ;C:YES, MATCHED
0743 23           1607: EX2:    INX  H                          ;NC:NO, FIND JUMP ADDR.
0744 be           1608:         CMP  M
0745 d2 3c 07     1609:         JNC  EX2
0748 23           1610:         INX  H                          ;BUMP TO NEXT TAB. ITEM
0749 d1           1611:         POP  D                          ;RESTORE STRING POINTER
074a c3 27 07     1612:         JMP  EX0                        ;TEST AGAINST NEXT ITEM
074d 3e 7f        1613: EX3:    MVI  A,07FH                     ;PARTIAL MATCH, FIND
074f 23           1614: EX4:    INX  H                          ;JUMP ADDR., WHICH IS
0750 be           1615:         CMP  M                          ;FLAGGED BY BIT 7
0751 d2 48 07     1616:         JNC  EX4
0754 7e           1617: EX5:    MOV  A,M                        ;LOAD HL WITH THE JUMP
0755 23           1618:         INX  H                          ;ADDRESS FROM THE TABLE
0756 6e           1619:         MOV  L,M
0757 e6 7f        1620:         ANI  7FH                        ;MASK OFF BIT 7
0759 67           1621:         MOV  H,A
075a f1           1622:         POP  PSW                        ;CLEAN UP THE GABAGE
075b e9           1623:         PCHL                            ;AND WE GO DO IT
                  1624: ;
                  1625: LSTROM:                                 ;ALL ABOVE CAN BE ROM
                  1626: ;       ORG  1000H                      ;HERE DOWN MUST BE RAM
(2000)            1627:         ORG  02000H
2000 +0001        1628: OCSW:   DS   1                          ;SWITCH FOR OUTPUT
2001 +0002        1629: CURRNT: DS   2                          ;POINTS TO CURRENT LINE
2003 +0002        1630: STKGOS: DS   2                          ;SAVES SP IN 'GOSUB'
2005 +0002        1631: VARNXT: DS   2                          ;TEMP STORAGE
2007 +0002        1632: STKINP: DS   2                          ;SAVES SP IN 'INPUT'
2009 +0002        1633: LOPVAR: DS   2                          ;'FOR' LOOP SAVE AREA
200b +0002        1634: LOPINC: DS   2                          ;INCREMENT
200d +0002        1635: LOPLMT: DS   2                          ;LIMIT
200f +0002        1636: LOPLN:  DS   2                          ;LINE NUMBER
2011 +0002        1637: LOPPT:  DS   2                          ;TEXT POINTER
2013 +0002        1638: RANPNT: DS   2                          ;RANDOM NUMBER POINTER
2015 +0002        1639: TXTUNF: DS   2                          ;->UNFILLED TEXT AREA
2017 +0002        1640: TXTBGN: DS   2                          ;TEXT SAVE AREA BEGINS
                  1641: ;       ORG  1366H
(3f87)            1642:         ORG  03F87H
3f87 +0000        1643: TXTEND: DS   0                          ;TEXT SAVE AREA ENDS
3f87 +0037        1644: VARBGN: DS   55                         ;VARIABLE @(0)
3fbe +0040        1645: BUFFER: DS   64                         ;INPUT BUFFER
3ffe +0001        1646: BUFEND: DS   1                          ;BUFFER ENDS
(b800)            1647:         ORG  0B800H
b800 +0001        1648: STKLMT: DS   1                          ;TOP LIMIT FOR STACK
(c000)            1649:         ORG  0C000H
c000 +0000        1650: STACK:  DS   0                          ;STACK STARTS HERE
                  1651: ;
(000d)            1652: CR      EQU  0DH
(000a)            1653: LF      EQU  0AH
                  1654: 
                  1655:         ;END

1655 lines, 1 errors, 0 warnings


SYMBOL TABLE:

START           : 0000 (0)
CRLF            : 000e (14)
SS1             : 0028 (40)
TV1             : 0058 (88)
TC1             : 0068 (104)
TC2             : 0073 (115)
TSTNUM          : 0077 (119)
TN1             : 007c (124)
QHOW            : 009f (159)
AHOW            : 00a0 (160)
HOW             : 00a6 (166)
OK              : 00ab (171)
WHAT            : 00ae (174)
SORRY           : 00b4 (180)
RSTART          : 00ba (186)
ST1             : 00bd (189)
ST2             : 00cd (205)
ST3             : 00d6 (214)
ST4             : 010b (267)
NEW             : 0132 (306)
STOP            : 013b (315)
RUN             : 0141 (321)
RUNNXL          : 0147 (327)
RUNTSL          : 0150 (336)
RUNSML          : 0157 (343)
TAB2M1          : 06a9 (1705)
GOTO            : 0160 (352)
LIST            : 016f (367)
LS1             : 0178 (376)
PRINT           : 0187 (391)
PR2             : 0192 (402)
PR0             : 019b (411)
PR1             : 01a3 (419)
PR3             : 01a9 (425)
PR6             : 01b2 (434)
PR8             : 01b6 (438)
GOSUB           : 01bf (447)
RETURN          : 01df (479)
FOR             : 01f8 (504)
FR1             : 0206 (518)
FR2             : 020e (526)
FR3             : 0212 (530)
FR4             : 0215 (533)
FR5             : 0218 (536)
FR7             : 022d (557)
FR8             : 024e (590)
NEXT            : 0253 (595)
NX0             : 025a (602)
NX3             : 0272 (626)
NX4             : 0284 (644)
NX1             : 0294 (660)
NX5             : 02a6 (678)
NX2             : 02a8 (680)
REM             : 02ac (684)
IFF             : 02b0 (688)
INPERR          : 02bf (703)
INPUT           : 02c9 (713)
IP1             : 02c9 (713)
IP2             : 02d7 (727)
IP3             : 02e7 (743)
IP4             : 0311 (785)
IP5             : 0318 (792)
DEFLT           : 0319 (793)
LET             : 031f (799)
LT1             : 0328 (808)
EXPR1           : 0329 (809)
XP11            : 032d (813)
XP12            : 0333 (819)
XP13            : 0339 (825)
XP14            : 0340 (832)
XP15            : 0348 (840)
XP16            : 034e (846)
XP17            : 0354 (852)
XP18            : 0356 (854)
EXPR2           : 036b (875)
XP21            : 0374 (884)
XP22            : 0377 (887)
XP23            : 037a (890)
XP24            : 0381 (897)
XP25            : 0392 (914)
XP26            : 0395 (917)
EXPR3           : 039f (927)
XP31            : 03a2 (930)
XP32            : 03bf (959)
XP33            : 03c7 (967)
XP34            : 03d2 (978)
XP35            : 03f1 (1009)
EXPR4           : 03ff (1023)
XP40            : 0403 (1027)
XP41            : 040c (1036)
PARN            : 0412 (1042)
XP42            : 0419 (1049)
XP43            : 041a (1050)
RND             : 041d (1053)
RA1             : 0438 (1080)
ABS             : 0448 (1096)
SIZE            : 0451 (1105)
DIVIDE          : 045e (1118)
DV1             : 0469 (1129)
DV2             : 046b (1131)
SUBDE           : 0474 (1140)
CHKSGN          : 047b (1147)
CHGSGN          : 047e (1150)
CKHLDE          : 0490 (1168)
CK1             : 0496 (1174)
SETVAL          : 0498 (1176)
SV1             : 04a8 (1192)
FIN             : 04ab (1195)
FI1             : 04b2 (1202)
FI2             : 04b9 (1209)
ENDCHK          : 04ba (1210)
QWHAT           : 04be (1214)
AWHAT           : 04bf (1215)
ERROR           : 04c2 (1218)
QSORRY          : 04eb (1259)
ASORRY          : 04ec (1260)
GETLN           : 04f2 (1266)
GL1             : 04f6 (1270)
GL3             : 051b (1307)
GL4             : 0528 (1320)
FNDLN           : 0530 (1328)
FNDLP           : 0538 (1336)
FL1             : 0538 (1336)
FNDNXT          : 054c (1356)
FL2             : 054d (1357)
FNDSKP          : 054e (1358)
PRTSTG          : 0558 (1368)
PS1             : 0559 (1369)
QTSTG           : 0564 (1380)
QT1             : 0569 (1385)
QT2             : 0572 (1394)
QT3             : 0576 (1398)
QT4             : 057e (1406)
QT5             : 0589 (1417)
PRTNUM          : 058a (1418)
PN1             : 0595 (1429)
PN2             : 059c (1436)
PN3             : 05ac (1452)
PN4             : 05ad (1453)
PN5             : 05b9 (1465)
PN6             : 05bf (1471)
PRTLN           : 05ca (1482)
MVUP            : 05dd (1501)
MVDOWN          : 05e6 (1510)
MD1             : 05ee (1518)
POPA            : 05f5 (1525)
PP1             : 060f (1551)
PUSHA           : 0611 (1553)
PU1             : 0637 (1591)
CONIN_ADDR      : f828 (63528)
CONOUT_ADDR     : f830 (63536)
CONSTAT_ADDR    : f820 (63520)
INIT            : 063a (1594)
PATLOP          : 063f (1599)
OC2             : 065c (1628)
OC3             : 0661 (1633)
CHKIO           : 066e (1646)
CHKIO_CNT       : 0675 (1653)
CI1             : 0689 (1673)
MSG1            : 068f (1679)
TAB1            : 069a (1690)
TAB2            : 06aa (1706)
TAB4            : 06ee (1774)
TAB5            : 0700 (1792)
TAB6            : 0706 (1798)
TAB8            : 070e (1806)
DIRECT          : 0724 (1828)
EXEC            : 0727 (1831)
EX0             : 0727 (1831)
EX1             : 0729 (1833)
EX2             : 073c (1852)
EX3             : 0746 (1862)
EX4             : 0748 (1864)
EX5             : 074d (1869)
LSTROM          : 0755 (1877)
OCSW            : 2000 (8192)
CURRNT          : 2001 (8193)
STKGOS          : 2003 (8195)
VARNXT          : 2005 (8197)
STKINP          : 2007 (8199)
LOPVAR          : 2009 (8201)
LOPINC          : 200b (8203)
LOPLMT          : 200d (8205)
LOPLN           : 200f (8207)
LOPPT           : 2011 (8209)
RANPNT          : 2013 (8211)
TXTUNF          : 2015 (8213)
TXTBGN          : 2017 (8215)
TXTEND          : 3f87 (16263)
VARBGN          : 3f87 (16263)
BUFFER          : 3fbe (16318)
BUFEND          : 3ffe (16382)
STKLMT          : b800 (47104)
STACK           : c000 (49152)
CR              : 000d (13)
LF              : 000a (10)


Total memory is 1884 bytes
